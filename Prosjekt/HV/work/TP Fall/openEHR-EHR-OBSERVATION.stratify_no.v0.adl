archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.stratify_no.v0

concept
	[at0000]	-- STRATIFY vurderingsverktøy for fallrisiko
language
	original_language = <[ISO_639-1::nb]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Micaela Thierley">
				["organisation"] = <"Helse Bergen, Norway">
				["email"] = <"micaela.thierley@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Micaela Thierley">
		["organisation"] = <"Helse Bergen HF, Seksjon for e-helse">
		["email"] = <"micaela.thierley@helse-bergen.no">
		["date"] = <"2014-07-04">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For screening for falls risk in patients over 65 years of age, and other adults with neurological or cognitive conditions or significant visual impairment.

Includes elements from \"Barthel ADL Index\"; \"Transfer\" and \"Mobility\", which are represented in a separate archetype.

The total score can either be entered manually (ie calculated and directly entered by the clinician), or automatically (ie calculation and entry is done automatically by a software application, based on the recorded scores).">
			use = <"Risk assessment of all patients over the age of 65 and other adults with neurological or cognitive conditions or significant visual impairment, within 24h of being admitted to a healtchare institution.

The 'Combined Transfer and Mobility Score' data element is the result of adding the scores for the 'Transfer' data element and the 'Mobility' data element from the Barthel Index archetype.">
			keywords = <"risk", "fall", "balance">
			misuse = <"Not to be used without simultaneous use of the Barthel archetype.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"Til bruk for kartlegging av fallrisiko for pasienter over 65 år og andre voksne med nevrologiske eller kognitive sykdomstilstander eller betydelige synshemninger.">
			use = <"Risikovurdering av alle pasienter over 65 år og andre voksne med nevrologiske eller kognitive sykdomstilstander eller betydelige synshemninger innen ett døgn i helseinstitusjon.

Poengene fra forflytning og rørlighet /gangfunksjon er et resultat fra feltene \"Forflytning mellom seng og stol\" og \"Mobilitet\" fra arketypen OBSERVATION.barthel_no. For å kunne gjøre denne registreringen må STRATIFY og Barthel-arketypene enten brukes i samme templat, eller de aktuelle verdiene fra en nylig Barthel-registrering gjenbrukes fra en tidligere registrering. Når arketypene brukes i samme templat kan en velge å kun bruke de to aktuelle feltene fra Barthel-arketypen dersom en ikke har behov for en fullstendig Barthel-score.

Den endelige summen kan registreres manuelt (det vil si regnet ut og lagt inn av klinikeren) eller automatisk (automatisk summering basert på de registrerte verdier).">
			misuse = <"Skal ikke brukes utenom samtidig bruk av arketypen Barthel ADL-indeks, eventuelt gjenbruk av verdier fra en tidligere registrering av denne.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Anne Pauline Anderssen, Helse Nord RHF, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway", "Silje Ljosland Bakke, Helse Bergen HF, Norway (Nasjonal IKT redaktør)", "Lars Bitsch-Larsen, Haukeland University Hospital, Bergen, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Kåre Flø, DIPS ASA, Norway", "Einar Fosse, UNN HF, Norwegian Centre for Integrated Care and Telemedicine, Norway", "Bente Gjelsvik, Helse Bergen, Norway", "Ingrid Heitmann, Oslo universitetssykehus HF, Norway", "Erling Are Hole, Helse Bergen, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Sabine Leh, Helse-Bergen, Norway", "Heather Leslie, Ocean Informatics, Australia", "Siv Marie Lien, DIPS ASA, Norway", "Hallvard Lærum, Oslo Universitetssykehus HF, Norway", "Lars Ivar Mehlum, Helse Bergen HF, Norway", "Hugo Nilssen, UNN HF K3K/Tromsø, Norway", "Bjørn Næss, DIPS ASA, Norway", "Rune Pedersen, Universitetssykehuset i Nord Norge, Norway", "Micaela Thierley, Helse Bergen, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["current_contact"] = <"Micaela Thierley, Helse Bergen, micaela.thierley@helse-bergen.no">
		["revision"] = <"0.0.1-alpha">
		["references"] = <"Oliver D, Britton M, Seed P,  Martin FC , Hopper A H. Development and evaluation of evidence based risk assessment tool (STRATIFY) to predict which elderly inpatients will fall: case-control and cohort studies. BMJ 1997;315:1049

Sekretariatet for nasjonalt pasientsikkerhetsprogram, Helsedirektoratet. Forebygging av fall i helseinstitusjoner [Internett]; [sitert 17.06.2015]. Tilgjengelig fra: http://www.pasientsikkerhetsprogrammet.no/no/I+trygge+hender/Innsatsomr%C3%A5der/Forebygging+av+fall+i+helseinstitusjoner.453.cms">
		["build_uid"] = <"c4036782-dd89-4209-8f0f-56c210854ca1">
		["MD5-CAM-1.0.1"] = <"CF993D39C0A2D8A8A84FCEEF88FA3D4A">
	>

definition
	OBSERVATION[at0000] matches {	-- STRATIFY vurderingsverktøy for fallrisiko
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {	-- Uspesifisert hendelse
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {	-- Fall de siste tre måneder
										value matches {
											0|[local::at0011], 	-- Nei
											1|[local::at0012]  	-- Ja
										}
									}
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Synshemminger
										value matches {
											0|[local::at0015], 	-- Nei
											1|[local::at0017]  	-- Ja
										}
									}
									ELEMENT[at0005] occurrences matches {0..1} matches {	-- Urolig atferd
										value matches {
											0|[local::at0013], 	-- Nei
											1|[local::at0014]  	-- Ja
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {	-- Hyppige toalettbesøk
										value matches {
											0|[local::at0018], 	-- Nei
											1|[local::at0019]  	-- Ja
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {	-- Total sum av "Mobililtet" og "Forflytning mellom seng og stol" fra Barthel ADL-indeks
										value matches {
											0|[local::at0020], 	-- 0-2, 5 eller 6 poeng
											1|[local::at0021]  	-- 3-4 poeng
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Poeng totalt
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..5|}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"STRATIFY Falls Risk Assessment Tool">
					description = <"Assessment tool for identifying falls risk in elderly patients admitted to hospital or other 24h healthcare institutions. This version of the tool is based on a modified Norwegian translation, which has been deployed by the The Norwegian Patient Safety Programme.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Falls during the last three months">
					description = <"Has the patient fallen during the last 3 months?">
					comment = <"Score 0 for false/no,
Score 1 for true/yes.">
				>
				["at0005"] = <
					text = <"Agitation">
					description = <"Does the patient show agitated, desoriented or confused behaviour?">
					comment = <"Score 0 for false/no,
Score 1 for true/yes.">
				>
				["at0006"] = <
					text = <"Visual impairments">
					description = <"Is the patient visually impaired to the extent that everyday function is affected?">
					comment = <"Score 0 for false/no,
Score 1 for true/yes.">
				>
				["at0007"] = <
					text = <"Frequent toileting">
					description = <"Do you think the patient is in need of especially frequent toileting?">
					comment = <"Score 0 for false/no,
Score 1 for true/yes.">
				>
				["at0009"] = <
					text = <"Total score">
					description = <"Sum of the scores from all the elements.">
					comment = <"0 = low risk, 
1 = moderate risk,
>2 = high risk.">
				>
				["at0010"] = <
					text = <"Combined sum of \"Transfer\" and \"Mobility\" scores from Barthel ADL Index">
					description = <"The points come from recordings made using the Barthel ADL Index archetype, from the elements \"Mobility\" and \"Transfer\".">
				>
				["at0011"] = <
					text = <"No">
					description = <"The patient hasn't had any falls during the last 3 months.">
				>
				["at0012"] = <
					text = <"Yes">
					description = <"The patient has had falls during the last 3 months.">
				>
				["at0013"] = <
					text = <"No">
					description = <"The patient doesn't show agitated behaviour.">
				>
				["at0014"] = <
					text = <"Yes">
					description = <"The patient shows agitated behaviour.">
				>
				["at0015"] = <
					text = <"No">
					description = <"The patient doesn't have significant visual impairments.">
				>
				["at0017"] = <
					text = <"*Ja(nb)">
					description = <"The patient has significant visual impairments.">
				>
				["at0018"] = <
					text = <"No">
					description = <"The patient isn't in need of frequent toileting.">
				>
				["at0019"] = <
					text = <"Yes">
					description = <"The patient is in need of frequent toileting.">
				>
				["at0020"] = <
					text = <"0-2, 5 or 6 points">
					description = <"0 points.">
				>
				["at0021"] = <
					text = <"3-4 points">
					description = <"1 point.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"STRATIFY vurderingsverktøy for fallrisiko">
					description = <"Vurderingsverktøy for identifisering av fallrisiko hos eldre pasienter på sykehus eller andre heldøgnsinstitusjoner. Denne versjonen er basert på den modifiserte norske oversettelsen som er tatt i bruk av det nasjonale pasientsikkerhetsprogrammet.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Uspesifisert hendelse">
					description = <"Standard, uspesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en template eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Fall de siste tre måneder">
					description = <"Har pasienten hatt noen fall i de siste 3 månedene?">
					comment = <"score 0 for Usann/ Nei, 
score 1 for Sann/ Ja.">
				>
				["at0005"] = <
					text = <"Urolig atferd">
					description = <"Viser pasienten urolig, uklar, desorientert eller forvirret atferd?">
					comment = <"score 0 for Usann/ Nei, 
score 1 for Sann/ Ja.">
				>
				["at0006"] = <
					text = <"Synshemminger">
					description = <"Er pasienten synsskadet/synshemmet i et slikt omfang at hverdagslige funksjoner er påvirket?">
					comment = <"score 0 for Usann/ Nei, 
score 1 for Sann/ Ja.">
				>
				["at0007"] = <
					text = <"Hyppige toalettbesøk">
					description = <"Oppfatter du at pasienten har spesielt behov av å besøke toalett ofte?">
					comment = <"score 0 for Usann/ Nei, 
score 1 for Sann/ Ja.">
				>
				["at0009"] = <
					text = <"Poeng totalt">
					description = <"Sum av poeng fra alle elementene. ">
					comment = <"0 = lav risko, 
1 = moderat risko, 
>2 = høy risko.">
				>
				["at0010"] = <
					text = <"Total sum av \"Mobililtet\" og \"Forflytning mellom seng og stol\" fra Barthel ADL-indeks">
					description = <"Poengene kommer fra registreringer gjort i Barthel ADL indeks fra punktene \"Mobililtet\" og \"Forflytning mellom seng og stol\".">
				>
				["at0011"] = <
					text = <"Nei">
					description = <"Pasienten har ikke hatt noen fall siste 3 måneder.">
				>
				["at0012"] = <
					text = <"Ja">
					description = <"Pasienten har hatt fall siste 3 måneder.">
				>
				["at0013"] = <
					text = <"Nei">
					description = <"Pasienten viser ikke urolig atferd.">
				>
				["at0014"] = <
					text = <"Ja">
					description = <"Pasienten viser urolig atferd.">
				>
				["at0015"] = <
					text = <"Nei">
					description = <"Pasienten har ikke store synshemminger.">
				>
				["at0017"] = <
					text = <"Ja">
					description = <"Pasienten har store synshemminger.">
				>
				["at0018"] = <
					text = <"Nei">
					description = <"Pasienten har ikke behov for hyppige toalettbesøk.">
				>
				["at0019"] = <
					text = <"Ja">
					description = <"Pasienten har behov for hyppige toalettbesøk.">
				>
				["at0020"] = <
					text = <"0-2, 5 eller 6 poeng">
					description = <"0 poeng.">
				>
				["at0021"] = <
					text = <"3-4 poeng">
					description = <"1 Poeng.">
				>
			>
		>
	>
