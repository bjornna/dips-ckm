archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.laboratory_test_panel.v1

concept
	[at0000]	-- Laboratory Test Panel
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen">
				["organisation"] = <"Oslo universitetssykehus HF">
				["email"] = <"varntzen@ous-hf.no">
			>
			accreditation = <"RN, Enterprise Architect TOGAF sertified">
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"freshEHR Clinical Informatics, UK">
		["email"] = <"ian@freshehr.com">
		["date"] = <"2014-04-18">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record laboratory test results as a single value or in a panel/battery format common to clinical pathology testing such as biochemistry, haematology and immunology.">
			use = <"To record laboratory test results as a single value or in a panel/battery format common to clinical pathology testing such as biochemistry, haematology and immunology. Normally used in conjunction with a parent Laboratory test result (Observation) archetype.

The name attribute of the Laboratory Test Panel/Result value element is normally replaced in a template or at run-time to carry the name of the specific analyte e.g. 'Haemoglobin' 


">
			keywords = <"laboratory", "pathology", "panel", "battery", "analyte">
			misuse = <"Should not be used to record Anatomical pathology macroscopic/microscopic findings.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å lagre prøveresultater fra laboratorier, enten som en enkeltverdi eller i grupper/pakker i et format som er vanlig for klinisk analyse, som biokjemi, hematologi og immunologi.">
			use = <"For å lagre prøveresultater fra laboratorier, enten som en enkeltverdi eller i grupper/pakker i et format som er vanlig for klinisk analyse, som biokjemi, hematologi og immunologi. Brukes vanligvis i sammenmeheng med en overordnet OBSERVATION-arketype for laboratorieprøvesvar.

Dataelementet \"Prøvesvar\" skal vanligvis omdøpes i templat eller i skjermbilde under kjøring i en applikasjon til å hete noe som er spesifikt for analysen, f.eks \"Hemoglobin\".
">
			keywords = <"laboratorie", "patologi", "prøvepakker", "analyse", "analyser">
			misuse = <"Bør ikke brukes for å lagre funn fra patologi-prøver der det er brukt makroskopiske eller mikroskopiske metoder.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", ...>
	other_details = <
		["references"] = <"Based on NEHTA 'Pathology Test' archetype. Available from: http://dcm.nehta.org.au/ckm/OKM.html#showarchetype_1013.1.839_8

Pathology (Data Specifications) Version 1.0 [Internet]. Sydney, Australia: National E-Health Transition Authority; 2007 May 29 [cited 2011 Jul 11]; Available at http://www.nehta.gov.au/component/docman/doc_download/962-pathology-v10.

Laboratory Technical Framework, Volume 3: Content, Revision 3.0 [Internet]. USA: IHE International; 2011 May 19; [cited 2011 Jul 11]. Available from: http://www.ihe.net/Technical_Framework/index.cfm#laboratory

Hl7 FHIR Observation resource: HL7 FHIR; Available from http://www.hl7.org/implement/standards/fhir/observation.html">
		["MD5-CAM-1.0.1"] = <"8C99F2422BB6DD54BBA27D923F2717C9">
	>

definition
	CLUSTER[at0000] matches {	-- Laboratory Test Panel
		items cardinality matches {1..*; unordered} matches {
			CLUSTER[at0002] occurrences matches {0..*} matches {	-- Laboratory result
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0001] occurrences matches {0..1} matches {*}
					ELEMENT[at0003] occurrences matches {0..*} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Reference range guidance
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Result status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0007, 	-- Registered
									at0008, 	-- Interim
									at0009, 	-- Final
									at0010, 	-- Amended
									at0011, 	-- Cancelled/Aborted
									at0012]	-- Not requested
								}
							}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Result status timestamp
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {	-- Other detail
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Laboratory Test Panel">
					description = <"Laboratory test result as a single value or in a panel/battery format common to clinical pathology testing.">
				>
				["at0001"] = <
					text = <"Result value">
					description = <"Actual value of the result. The name of this element is normally replaced in a template or at run-time to relfect the actual analyte e.g. Serum sodium and will often be coded.">
					hl7v2_mapping = <"OBX.2,OBX.5,OBX.6; OBX-3-observation identifier">
					fhir_mapping = <"Observation.result; Observation.name">
				>
				["at0002"] = <
					text = <"Laboratory result">
					description = <"Specific detailed result, including both the value of the result item, and additional information that may be useful for clinical interpretation.">
				>
				["at0003"] = <
					text = <"Comment">
					description = <"Comment about the Result.">
					hl7v2_mapping = <"NTE.3">
					fhir_mapping = <"Observation.comments">
				>
				["at0004"] = <
					text = <"Reference range guidance">
					description = <"Additional advice on the applicability of the reference range.">
				>
				["at0005"] = <
					text = <"Result status">
					description = <"The status of the result value.">
					hl7v2_mapping = <"OBX-11-observation result status">
					fhir_mapping = <"status">
				>
				["at0006"] = <
					text = <"Result status timestamp">
					description = <"The date and/or time that the entire result was issued for the recorded ‘Result status’.">
					hl7v2_mapping = <"OBX-22 and/or OBX-19">
					fhir_mapping = <"Observation.issued">
				>
				["at0007"] = <
					text = <"Registered">
					description = <"No result yet available.">
				>
				["at0008"] = <
					text = <"Interim">
					description = <"This is an initial or interim result: data may be missing or verification not been performed.">
				>
				["at0009"] = <
					text = <"Final">
					description = <"The result is complete and verified by the responsible pathologist.">
				>
				["at0010"] = <
					text = <"Amended">
					description = <"The result has been modified subsequent to being Final, and is complete and verified by the responsible pathologist.">
				>
				["at0011"] = <
					text = <"Cancelled/Aborted">
					description = <"The result is unavailable because the test was not started or not completed.">
				>
				["at0012"] = <
					text = <"Not requested">
					description = <"The result is not available as the test was not requested.">
				>
				["at0013"] = <
					text = <"Other detail">
					description = <"Further details including the specimen for the panel or a further nested panel.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Laboratorie Test Panel">
					description = <"Laboratorieprøveresultat som en enkeltverdi eller i grupper/pakker som er vanlig i kliniske prøveanalyser.">
				>
				["at0001"] = <
					text = <"Analysesvar">
					description = <"Faktisk verdi av prøvesvaret. Navnet på dette dataelementet er vanligvis oversatt i templat eller i applikasjon, slik at det samsvarer med den faktiske analysen. For eksempel \"S-Na\" eller \"Serum Natrium\". Det vil også som regel være kodet i et kodeverk.
">
				>
				["at0002"] = <
					text = <"Analysesvar">
					description = <"Spesifikt detaljert svar, som inkluderer både verdien og tilleggsinformasjon som kan være nyttig for klinisk tolkning.">
				>
				["at0003"] = <
					text = <"Kommentar">
					description = <"Kommentar til svaret.">
				>
				["at0004"] = <
					text = <"Veiledning til referanseverdi">
					description = <"Tilleggsinformasjon med råd for anvendeligheten for referanseverdi.">
				>
				["at0005"] = <
					text = <"Svarstatus">
					description = <"Prøvesvarets status">
				>
				["at0006"] = <
					text = <"Tidsstempel svarstatus">
					description = <"Dato og/eller tidspunkt hele svaret ble utstedt for \"Svarsstatus\".">
				>
				["at0007"] = <
					text = <"Registrert">
					description = <"Ikke tilgjengelig svar ennå.">
				>
				["at0008"] = <
					text = <"Foreløpig">
					description = <"Dette er et midlertidig eller foreløpig svar. Data kan mangle, være utilstrekkelig eller at svaret ikke er verifisert.">
				>
				["at0009"] = <
					text = <"Endelig">
					description = <"Svaret er komplett og verifisert av ansvarlig laboratorielege.">
				>
				["at0010"] = <
					text = <"Endret">
					description = <"Svaret er modifisert/endret etter å ha vært i status Endelig, og er komplett og verifisert av ansvarlig laboratorielege.">
				>
				["at0011"] = <
					text = <"Kansellert">
					description = <"Svaret er ikke tilgjengelig fordi analysen ikke ble påbegynt eller ferdigstilt.">
				>
				["at0012"] = <
					text = <"Ikke bestilt">
					description = <"Svaret er ikke tilgjengelig siden analysen ikke ble bestilt.">
				>
				["at0013"] = <
					text = <"Andre detaljer">
					description = <"Ytterligere detaljer, inkludert informasjon om prøveeksemplaret i analysepakker, eller når materialet deles opp i flere forskjellige analyser.">
				>
			>
		>
	>
