archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.medication_amount.v1

concept
	[at0000]	-- Medication amount
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen">
				["organisation"] = <"Oslo universitetssykehus HF">
			>
			accreditation = <"RN, Enterprise Architect">
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["organisation"] = <"Ocean Informatics, UK">
		["email"] = <"ian.mcnicoll@oceaninformatics.com">
		["date"] = <"2012-12-03">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To describe the amount or strength of a substance for reuse in different ENTRY classes.">
			use = <"In INSTRUCTION medication, ACTION medication for amount ordered, dispensed, administered.">
			keywords = <"amount", "dose", "dosing">
			misuse = <"">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å beskrive menge eller styrke på et virkestoff for å gjenbrukes i forskjellige ENTRY-klasser.">
			use = <"Bruk i INSTRUCTION.medication, ACTION.medication for mengde foreskrevet, utdelt og administrert.">
			keywords = <"mengde", "dose", "dosering", "styrke">
			misuse = <"">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"CommitteeDraft">
	other_contributors = <"Sam Heard, Ocean Informatics, Australia", ...>
	other_details = <
		["MD5-CAM-1.0.1"] = <"228FC457FABC8ABB61BCF3F949B208F7">
	>

definition
	CLUSTER[at0000] matches {	-- Medication amount
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Amount
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::380]>
						list = <
							["1"] = <
								units = <"1">
								magnitude = <|>=0.0|>
							>
						>
					>
					DV_INTERVAL<DV_QUANTITY> matches {
						upper matches {
							C_DV_QUANTITY <
							>
						}
						lower matches {
							C_DV_QUANTITY <
								property = <[openehr::380]>
								list = <
									["1"] = <
										units = <"1">
										magnitude = <|>=0.0|>
									>
								>
							>
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Dose unit
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {[ac0001]}		-- Dose Unit
					}
				}
			}
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0008] occurrences matches {0..1} matches {	-- Ratio numerator
				items cardinality matches {1..*; unordered} matches {
					use_node ELEMENT occurrences matches {0..1} /items[at0001]	-- /items[Amount]
					use_node ELEMENT occurrences matches {0..1} /items[at0002]	-- /items[Dose unit]
				}
			}
			CLUSTER[at0007] occurrences matches {0..1} matches {	-- Ratio denominator
				items cardinality matches {1..*; unordered} matches {
					use_node ELEMENT occurrences matches {0..1} /items[at0001]	-- /items[Amount]
					use_node ELEMENT occurrences matches {0..1} /items[at0002]	-- /items[Dose unit]
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Medication amount">
					description = <"The amount or strength of a medication or substance for medication orders, administrations, dispensing etc.">
				>
				["at0001"] = <
					text = <"Amount">
					description = <"The value of the amount of medication as a real number e.g 1, 1.5, 0.125. ">
				>
				["at0002"] = <
					text = <"Dose unit">
					description = <"The dose unit of this medication amount.">
				>
				["at0003"] = <
					text = <"Description">
					description = <"Free text description of the amount which may consist of the amount value and amount dose unit.">
				>
				["at0007"] = <
					text = <"Ratio denominator">
					description = <"The denominator value where the amount is described as a ratio e.g. 5mls where the amount is 2mg/ 5mls.">
				>
				["at0008"] = <
					text = <"Ratio numerator">
					description = <"The numerator value where the amount is described as a ratio e.g. 2mg where the amount is 2mg/ 5mls.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Medikamentmengde">
					description = <"Mengden eller styrken til et medikament eller virkestoff for medikamentforordning, utdeling, administrering, etc.">
				>
				["at0001"] = <
					text = <"Mengde">
					description = <"Tall for mengde av medikament, som reelt tall, f.eks 1, 1.5, 0.125.">
				>
				["at0002"] = <
					text = <"Enhet">
					description = <"Hvilken enhet som gjelder denne medikasjonen.">
				>
				["at0003"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av mengden, som kan bestå av både mengde og styrke.">
				>
				["at0007"] = <
					text = <"Nevner">
					description = <"Nevner-verdien der mengden er beskrevet i et styrkeforhold, for eksempel er nevneren 5ml når styrkeforholdet er 2mg/5ml.">
				>
				["at0008"] = <
					text = <"Teller">
					description = <"Teller-verdien der mengden er beskrevet i et styrkeforhold, for eksempel er telleren 2 mg når styrkeforholdet er 2mg/5ml.">
				>
			>
		>
	>
	constraint_definitions = <
		["en"] = <
			items = <
				["ac0001"] = <
					text = <"Dose Unit">
					description = <"Tablets, Capsules, Sachets, mg, L etc.">
				>
			>
		>
		["nb"] = <
			items = <
				["ac0001"] = <
					text = <"Enhet">
					description = <"Tabletter, kapsler, mg, liter, gram, etc">
				>
			>
		>
	>

