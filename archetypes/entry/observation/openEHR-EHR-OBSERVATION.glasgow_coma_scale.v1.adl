archetype (adl_version=1.4; uid=9b384e82-6010-4d44-9bb1-9000ac3362d9)
	openEHR-EHR-OBSERVATION.glasgow_coma_scale.v1

concept
	[at0000]	-- Glasgow coma scale
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand/Silje Ljosland Bakke">
				["organisation"] = <"Helse Bergen HF/Nasjonal IKT HF">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2007-03-13">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record clinical responses of the subject of care to defined stimuli as an objective assessment of the level of consciousness.">
			use = <"Use to record clinical responses of an adult subject of care to stimuli. It is commonly used to establish a baseline conscious state and neurological function assessment and/or to detect patients who may require immediate medical intervention.

The Glasgow coma scale has three subscales E (eye), V (verbal) and M (motor). In clinical practice all three subscales are reported individually plus the 'Total score', if applicable.

A recorded response for each of E, V and M is mandatory. If a response cannot be tested, then the 'Not Applicable' null flavour should be recorded; do not use the 'None' ordinal value to record a missing component. Details about the reason for not being able to test a response can be recorded in the 'Confounding factors' data element.

The 'Total score' can be derived as the sum of the recorded eye, motor and verbal response scores. It is not appropriate to report a 'Total score' when one or more components are not testable because the score will be artificially low - in this situation record the EVM profile instead.

The three response values are considered separately as well as their sum. The 'EVM profile' can be derived as a concatenation of each of the recorded eye, motor and verbal response scores. For example, E3 V4 M2 represents the conscious state of a subject who opens eyes to speech, utters incomprehensible sounds and has an extensor response to stimulation.

The minimum possible 'Total score' value is 3 (equivalent to E1 V1 M1) and the maximum possible is 15 (equivalent to E4 V5 M6).

In practical use, Glasgow coma scale is recorded as one component of clinical monitoring, using sequential and repeated point-in-time measurements. Date and time should be recorded for each measurement, as well as any factors that may influence interpretation of changes. Changes in 'Total score' or any E, V or M values may have as much clinical significance as the value recorded initially.">
			keywords = <"response", "motor", "verbal", "eye", "stimulus", "glasgow", "coma", "scale", "neurological", "responsiveness", "EMV", "conscious", "GCS", "trauma", "central nervous system", "consciousness">
			misuse = <"Not to be used for assessing infants and young children - an archetype for Paediatric Glasgow Coma Scale will be required to ensure that the eye, motor and verbal response choices are appropriate for the age and ability of the child.">
			copyright = <"© openEHR Foundation">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere klinisk respons på spesifikk stimuli, som en objektiv vurdering av bevissthetsnivået til en pasient.">
			use = <"Brukes for å registrere den kliniske responsen på spesifikke stimuli hos en voksen pasient. Brukes vanligvis for å etablere en basis for bevissthetsnivå og nevrologisk funksjonsnivå og/eller for å fange opp pasienter som trenger en øyeblikkelig medisinsk intervensjon.

Glasgow coma scale har tre subskalaer Ø (Åpning av øynene), V (Verbal respons) og M (Motorisk respons). I klinisk praksis rapporteres alle de tre subskalaene individuelt, samt \"Totalskår\" dersom aktuelt.

En registrert respons på Ø, V og M er obligatorisk. I tilfeller der en respons ikke kan testes, skal null-verdien \"Not applicable\" registreres. Bruk ikke ordinalverdien \"Ingen\" for å registrere en manglende komponent. Detaljer om hvorfor en ikke var i stand til å utføre en eller flere av testene kan registreres i dataelementet \"Konfunderende faktorer\".

\"Totalskår\" beregnes som en sum av verdiene registrert i datelementene for \"Åpning av øynene\", \"Verbal respons\" og \"Motorisk respons\". En skal ikke rapportere \"Totalskår\" hvis en eller flere komponenter ikke lar seg teste, fordi skåringen da vil bli kunstig lav - i slike situasjoner benyttes ØVM-profilen i stedet.

De tre responsverdiene vurderes separat og som en totalskår. \"ØVM-profilen\" kan utledes som en sammensetning av hver enkelt av de registrerte skåringene for \"Åpning av øynene\", \"Verbal respons\" og \"Motorisk respons\". For eksempel vil Ø3 V4 M2 representere bevissthetstilstanden til et individ som åpner øyne ved tiltale, har en usammehengende tale, og reagerer med ekstensjon ved smertestimulering.

En totalskår kan som et minimum ha verdien 3 (Tilsvarer Ø1, V1, M1) og en maksimumsverdi på 15 (Ø4, V5, M6).

I praksis registreres Glasgow coma scale som en del av klinisk monitorering, målingene utføres sekvensielt og som gjentagende tidspunktmålinger. Dato og tid bør registreres for hver enkelt måling, på samme måte som faktorer som kan påvirke tolkning av målingen. Endringer i \"Totalskår\" eller verdiene Ø, V eller M har vanligvis en like stor klinisk betydning som verdien som først ble registrert.">
			keywords = <"respons", "motorikk", "verbal", "øye", "stimuli", "koma", "skala", "nevrologisk", "responsitivitet", "ØVM", "ØMV", "Bevisst", "GCS", "traume", "sentralnervesystemet", "bevisstløs", "CNS", "bevissthet">
			misuse = <"Brukes ikke for å vurdere spedbarn og små barn - en vil da måtte bruke en arketype for Paediatric Glasgow Coma Scale for å sikre at valgene knyttet til øyne, motorikk og verbal respons valg er best tilpasset barnets alder og evner.">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Tomas Alme, DIPS ASA, Norway", "Nadim Anani, Karolinska Institutet, Sweden", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Kristian Berg, Universitetssykehuset Nord Norge, Norway", "Lars Bitsch-Larsen, Haukeland University hospital, Norway", "Ole Andreas Bjordal, Webmed, Norway", "Martin Boeker, Medical Center - University of Freiburg, Germany", "Marja Buur, Medisch Centrum Alkmaar/ Code24, Netherlands", "Margaret Campbell, Queensland Health, Australia", "Rong Chen, Cambio Healthcare Systems, Sweden", "Bjørn Christensen, Helse Bergen HF, Norway", "Stephen Chu, Queensland Health, Australia", "Tamsin Cockayne, Australia", "Marc Cotran, identity vision systems, Canada", "Tom Dahlberg, Helgelandssykehuset HF, Fagstab, Norway", "Lisbeth Dahlhaug, Helse Midt - Norge IT, Norway", "Angela de Zwart, Orion Health, New Zealand", "Einar Fosse, National Centre for Integrated Care and Telemedicine, Norway", "Sebastian Garde, Ocean Informatics, Germany", "Christian Ghan, The Chris O'Brien Lifehouse at RPA, Australia", "Soon Ghee Yap, Singapore General Hospital, Singapore", "William Goossen, Results 4 Care, Netherlands", "Heather Grain, Llewelyn Grain Informatics, Australia", "Birger Haarbrandt, Hannover Medical School, Germany", "Sam Heard, Ocean Informatics, Australia", "Stig Erik Hegrestad, Helse Førde, Norway", "Erling Are Hole, Helse Bergen, Norway", "Hilde Hollås, DIPS ASA, Norway", "Oliver Hosking, Remote Health NT, Australia", "Evelyn Hovenga, EJSH Consulting, Australia", "Eugene Igras, IRIS Systems, Inc., Canada", "Sundaresan Jagannathan, Scottish NHS, United Kingdom", "Karl Trygve Kalleberg, Oslo Universitetssykehus, Norway", "Konstantinos Kalliamvakos, Cambio Healthcare Systems, Sweden", "Lars Karlsen, DIPS ASA, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Shinji Kobayashi, Kyoto University, Japan", "Sergey Kovalenko, Chelyabinsk Regional Children Hospital, Russia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Siv Marie Lien, DIPS ASA, Norway", "Hallvard Lærum, Direktoratet for e-helse, Norway", "Luis Marco Ruiz, Norwegian Center for Integrated Care and Telemedicine, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (Editor)", "Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Bjørn Næss, DIPS ASA, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Michael Reynolds, Great Lakes Pediatric Associates, PLLC, United States", "Hossein Riazi, Iran", "Arturo Romero, SESCAM, Spain", "Jussara Rotzsch, UNB, Brazil", "Anoop Shah, University College London, United Kingdom", "Tony Shannon, NHS, United Kingdom", "Line Silsand, Universitetssykehuset i Nord-Norge, Norway", "Gary Sinclair, NT DoH, Australia", "Annette Hole Sjøborg, DIPS ASA, Norway", "Tim Sturgill, United States", "Roy Støle, OUS, Norway", "Terje Sundstrøm, Helse Bergen HF, Norway", "Øystein Tveiten, Helse-Bergen, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)", "Stian Torleif Varpe, Helse Bergen, Norway", "Thomas Wilson, Finnmarkssykehuset HF Klinikk Hammerfest, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Teasdale G. Glasgow Coma Scale: The Glasgow structured approach to Assessment of the Glasgow Coma Scale [Internet]. Sir Graham Teasdale; 2014 {cited 2015 Apr 20]. Available from: http://www.glasgowcomascale.org/ and http://www.glasgowcomascale.org/downloads/GCS-Assessment-Aid.pdf.

Glasgow Coma Scale, draft archetype, NEHTA Clinical Knowledge Manager [Internet]. Australia: National eHealth Transition Authority. Authored: 2007 Mar 13. Available at: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.1160 (accessed 2015 Mar 05).

Crippen DW. Head Trauma - Presentation: Medscape Reference: Drugs, Diseases & Procedures [Internet]. WebMD LLC: c1994-2013; [updated 2012 Jun 21; accessed 2015 Mar 05]. Available from: http://emedicine.medscape.com/article/433855-overview#a0112.

Teasdale G, Jennett B. Assessment of coma and impaired consciousness. A practical scale. Lancet 1974,2:81-84.

Teasdale GM, Murray L. Revisiting the Glasgow Coma Scale and Coma Score. Intensive Care Medicine. 2000; 26: 153-154.

V3 DCModels R1 I1 2010 Sep - Glasgow Coma Scale v0.75 [Internet]. Health Level Seven International. Published Jan 2014. Available at: https://github.com/DetailedClinicalModels/Detailed-Clinical-Models/blob/master/Glasgow%20Coma%20Scale%20(GCS)/org.hl7.GlasgowComaScale(15pointversion)English-v0.75.pdf (accessed 22 Mar 2015)

Young, G. B., Aminoff, M. J., Hockberger, R. S., & Wilterdink, J. L. (May 27, 2015). Stupor and coma in adults. UpToDate [Internett]. Available at: https://www.uptodate.com/contents/stupor-and-coma-in-adults [Accessed 2017 Mar 20].

NEL - Nevrologiske prosedyrer (30.07.15) Overvåkning og Glasgow Coma Scale. Hentet 21.03.17 fra http://nevro.legehandboka.no/handboken/nel/skjemakalkulatorer/skjema/akutt/gcs-og-overvakningsskjema/">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"5C65ACE91DFA61E185F8A858A1F6C1B1">
		["build_uid"] = <"ec1d038d-812a-4069-aef5-41ac72b3b810">
		["revision"] = <"1.0.0">
	>

definition
	OBSERVATION[at0000] matches {	-- Glasgow Coma Scale
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {	-- Tidspunkt
						data matches {
							ITEM_TREE[at0003] matches {	-- Tree
								items cardinality matches {3..*; unordered} matches {
									ELEMENT[at0009] matches {	-- Åpning av øynene (Ø)
										value matches {
											1|[local::at0010], 	-- Ingen reaksjon
											2|[local::at0011], 	-- Ved smertestimulering
											3|[local::at0012], 	-- Ved tiltale
											4|[local::at0013]  	-- Spontant
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0007] matches {	-- Verbal respons (V)
										value matches {
											1|[local::at0014], 	-- Ingen reaksjon
											2|[local::at0015], 	-- Uforståelige lyder
											3|[local::at0016], 	-- Usammenhengende tale
											4|[local::at0017], 	-- Forvirret, desorientert
											5|[local::at0018]  	-- Orientert
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0008] matches {	-- Motorisk respons (M)
										value matches {
											1|[local::at0019], 	-- Ingen reaksjon
											2|[local::at0020], 	-- Ekstensjon ved smerte
											3|[local::at0021], 	-- Fleksjon ved smerte
											4|[local::at0022], 	-- Avverger smerte
											5|[local::at0023], 	-- Lokaliserer smerte
											6|[local::at0024]  	-- Følger oppfordringer
										}
										null_flavour existence matches {0..1} matches {
											DV_CODED_TEXT matches {
												defining_code matches {[openehr::273]}
											}
										}
									}
									ELEMENT[at0026] occurrences matches {0..1} matches {	-- Totalskår
										value matches {
											DV_COUNT matches {
												magnitude matches {|3..15|}
											}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {	-- ØVM-profil
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0037] occurrences matches {0..*} matches {	-- Kommentar
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0040] matches {	-- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0041] occurrences matches {0..*} matches {	-- Konfunderende faktorer
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0038] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {	-- Utvidelse
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0042] occurrences matches {0..1} matches {	-- Smertestimuli
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Glasgow coma scale">
					description = <"Fifteen point scale used to assess impairment of consciousness in response to defined stimuli. More correctly known as the Modified Glasgow coma scale.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Best verbal response (V)">
					description = <"Best verbal response to test stimulus.">
					comment = <"Most commonly, the score for verbal response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care is intubated, then the \"Not Applicable\" null flavour should be recorded.">
				>
				["at0008"] = <
					text = <"Best motor response (M)">
					description = <"Best motor response to test stimulus.">
					comment = <"Most commonly, the score for motor response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot move their limbs due to injury or paralysis, then the \"Not Applicable\" null flavour should be recorded.">
				>
				["at0009"] = <
					text = <"Best eye response (E)">
					description = <"Best response of eyes to test stimulus.">
					comment = <"Most commonly, the score for eye response will be selected from one of the ordinal values, however if a response cannot be tested, for example if the subject of care cannot physically open their eyes due to other injuries, then the \"Not Applicable\" null flavour should be recorded.">
				>
				["at0010"] = <
					text = <"None">
					description = <"No eye opening at any time, no interfering factor. For example: eyes closed by local swelling.">
				>
				["at0011"] = <
					text = <"To pressure">
					description = <"Eyes opening after finger tip stimulus.">
				>
				["at0012"] = <
					text = <"To sound">
					description = <"Eyes opening after spoken or shouted request. Not to be confused with wakening of a sleeping person.">
				>
				["at0013"] = <
					text = <"Spontaneous">
					description = <"Eyes open before stimulus.">
				>
				["at0014"] = <
					text = <"None">
					description = <"No audible response, no interfering factor. For example: intubation; profound deafness.">
				>
				["at0015"] = <
					text = <"Sounds">
					description = <"Only moans/groans.">
				>
				["at0016"] = <
					text = <"Words">
					description = <"Intelligible single words.">
				>
				["at0017"] = <
					text = <"Confused">
					description = <"Not orientated but communicates coherently.">
				>
				["at0018"] = <
					text = <"Orientated">
					description = <"Correctly gives name, place and date.">
				>
				["at0019"] = <
					text = <"None">
					description = <"No movement in arms/legs, no interfering factor. For example: paralysed.">
				>
				["at0020"] = <
					text = <"Extension">
					description = <"Decerebrate extension of arms and/or legs in response to stimuli. For example: extends arm at elbow.">
				>
				["at0021"] = <
					text = <"Abnormal flexion">
					description = <"Slow, decorticate flexion of arms and/or legs. For example: bends arm at elbow, but features predominantly abnormal.">
				>
				["at0022"] = <
					text = <"Normal flexion">
					description = <"Rapid flexion in response to stimuli but features predominantly normal. For example: flexion of wrist when supra-orbital pressure applied; pulls part of body away when nailbed pinched.">
				>
				["at0023"] = <
					text = <"Localising">
					description = <"Purposeful flexion towards painful stimuli. For example: brings hand above the clavicle when supra-orbital pressure is applied.">
				>
				["at0024"] = <
					text = <"Obeys commands">
					description = <"Follows verbal request for movement.">
				>
				["at0026"] = <
					text = <"Total score">
					description = <"The sum of the ordinal scores recorded for each of the three component responses.">
					comment = <"The Total Score may be derived as the sum of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. Do not report a total score when one or more components are not testable because the score will be artificially low - in this situation record the EVM profile.">
				>
				["at0030"] = <
					text = <"EVM profile">
					description = <"Alternative assessment to 'Total Score' where the Eye Verbal Motor profile is expressed as three discrete components.">
					comment = <"The EMV Score may be automatically derived as a concatenation of the three response data elements and, if so, should be validated by the clinical information system against the individual scores entered by the clinician to ensure there is no conflict or inconsistency. The 'Not applicable' null flavour value should be recorded as 'NT', for 'not tested' as per http://www.glasgowcomascale.org/.">
				>
				["at0037"] = <
					text = <"Comment">
					description = <"Additional narrative about the measurement of the scale not captured in other fields.">
					comment = <"Record the reasons for inability to test in the 'Confounding factors' data element.">
				>
				["at0038"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0039"] = <
					text = <"Extension">
					description = <"Additional information required to capture local content or to align with other reference models/formalisms.">
					comment = <"For example: local information requirements or additional metadata to align with FHIR or CIMI equivalents.">
				>
				["at0040"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0041"] = <
					text = <"Confounding factors">
					description = <"Narrative record of factors that may have contributed to the GCS scores.">
					comment = <"For example: deafness; aphasia; language issues; use of sedatives; hypothermia; or paralysis, severe facial/eye swelling or tracheal intubation as reasons for inability to record any one of the ordinal E, V or M assessments.">
				>
				["at0042"] = <
					text = <"Pain stimulus">
					description = <"Description of the type of pain stimulus used.">
					comment = <"For example: Trapezius pinch for central  pain stimulation and Nail pinch for peripheral pain stimulation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Glasgow Coma Scale">
					description = <"En femtenpoengs skala som brukes for å anslå bevissthetsnivå ut fra et definert stimuli.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Tidspunkt">
					description = <"Spesifisert tidspunkt eller tidsintervall som kan defineres mer eksplisitt i en templat eller i en applikasjon.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0007"] = <
					text = <"Verbal respons (V)">
					description = <"Beste verbal respons på stimuli.">
					comment = <"Vanligvis vil skår for verbalrespons velges fra en av ordinalverdiene, men i tilfeller hvor verbalrespons ikke kan testes, for eksempel om pasienten er intubert, skal null-verdien \"Not Applicable\" registreres.">
				>
				["at0008"] = <
					text = <"Motorisk respons (M)">
					description = <"Beste motoriske respons på stimuli.">
					comment = <"Vanligvis vil skår for motorisk respons velges fra en av ordinalverdiene, men i tilfeller hvor en respons ikke kan testes, for eksempel om pasienten ikke kan bevege lemmene som følge av skade eller lammelse, skal null-verdien \"Not Applicable\" registreres.">
				>
				["at0009"] = <
					text = <"Åpning av øynene (Ø)">
					description = <"Beste øyerespons ved stimuli.">
					comment = <"Vanligvis vil skår for øyerespons velges fra en av ordinalverdiene, men i tilfeller hvor øyerespons ikke kan testes, for eksempel om pasienten ikke fysisk kan åpne øynene grunnet skade, skal null-verdien \"Not Applicable\" registreres.">
				>
				["at0010"] = <
					text = <"Ingen reaksjon">
					description = <"Øynene åpnes ikke, ingen forstyrrende faktorer som for eksempel lukkede øyne på grunn av lokal hevelse.">
				>
				["at0011"] = <
					text = <"Ved smertestimulering">
					description = <"Åpner øynene på smerte. For eksempel ved supraorbitalt trykk.">
				>
				["at0012"] = <
					text = <"Ved tiltale">
					description = <"Øynene åpnes ved tiltale eller ved roping. Må ikke forveksles med vekking av en sovende person.">
				>
				["at0013"] = <
					text = <"Spontant">
					description = <"Åpner øynene av seg selv uten stimuli.">
				>
				["at0014"] = <
					text = <"Ingen reaksjon">
					description = <"Ingen hørbar respons og ingen forstyrrende faktorer, som for eksempel intubasjon eller døvhet.">
				>
				["at0015"] = <
					text = <"Uforståelige lyder">
					description = <"Bare stønning/jamring.">
				>
				["at0016"] = <
					text = <"Usammenhengende tale">
					description = <"Forståelige enkeltord.">
				>
				["at0017"] = <
					text = <"Forvirret, desorientert">
					description = <"Ikke orientert, men kommuniserer sammenhengende.">
				>
				["at0018"] = <
					text = <"Orientert">
					description = <"Oppgir riktig navn, sted og dato.">
				>
				["at0019"] = <
					text = <"Ingen reaksjon">
					description = <"Ingen bevegelse i armer/bein, ingen forstyrrende faktorer, som for eksempel lammelser.">
				>
				["at0020"] = <
					text = <"Ekstensjon ved smerte">
					description = <"Decerebrert ekstensjon av armer og/eller bein som respons på stimuli. For eksempel kan armen ekstenderes ved albuen.">
				>
				["at0021"] = <
					text = <"Fleksjon ved smerte">
					description = <"Abnorm/ unormal fleksjon. Sakte, decortikert fleksjon av armer og/eller bein. For eksempel kan armen bøyes ved albuen, men trekkene er hovedsakelig abnormale.">
				>
				["at0022"] = <
					text = <"Avverger smerte">
					description = <"Avvergereaksjon. Hurtig fleksjon som respons på stimuli, men trekkene er hovedsakelig normale. For eksempel kan håndleddet bøyes eller trekkes til seg når det utføres supraorbitalt trykk.">
				>
				["at0023"] = <
					text = <"Lokaliserer smerte">
					description = <"Målrettet fleksjon mot smertestimuli. For eksempel kan hånden løftes til over kragebeinet når det utføres supraorbitalt trykk.">
				>
				["at0024"] = <
					text = <"Følger oppfordringer">
					description = <"Følger oppfordring for bevegelse.">
				>
				["at0026"] = <
					text = <"Totalskår">
					description = <"Sum av verdiene som er registrert for hver enkelt av de tre undersøkelsesdelene.">
					comment = <"Totalskåren kan regnes ut ved å summere de tre undersøkelsesdelene. Totalskår skal ikke regnes ut når en eller flere undersøkelsesdeler ikke er mulig å gjennomføre, fordi skåren da vil bli kunstig lav. I disse situasjonene skal ØVM-profilen registreres.">
				>
				["at0030"] = <
					text = <"ØVM-profil">
					description = <"Alternativ til Totalskår der responsen til Øye, Verbal og Motorisk er uttrykt som tre separate verdier.">
					comment = <"ØVM-profilen kan genereres automatisk ved å sette sammen de tre undersøkelsesdelene. Hvis dette gjøres, bør resultatet sammenlignes mellom det som det kliniske informasjonssystemet presenterer og det som klinikere har registrert for å utelukke konflikter eller inkonsistens. Nullverdien \"Not applicable\" registreres som \"IT\", for \"ikke testet\", som beskrevet på http://www.glasgowcomascale.org/.">
				>
				["at0037"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekst om målingen som ikke er fanget i andre felt.">
					comment = <"Hvis hele eller deler av testen ikke var gjennomførbar, skal årsak til dette registreres i dataelementet \"Konfunderende faktorer\".">
				>
				["at0038"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0039"] = <
					text = <"Utvidelse">
					description = <"Ytterligere informasjon som er nødvendig for å sammenstille med andre referansemodeller/formalismer.
">
					comment = <"For eksempel: lokale informasjonskrav eller ekstra metadata for å samsvare med FHIR eller CIMI ekvivalenter.">
				>
				["at0040"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0041"] = <
					text = <"Konfunderende faktorer">
					description = <"Fritekst om faktorer som kan ha bidratt til GCS-skåringene.">
					comment = <"For eksempel: Døvhet, afasi, språkproblemer, bruk av beroligende medisin, hypotermi eller paralyser, alvorlige hevelser i ansikt/øyn, alkoholpåvirkning eller trakeal intubasjon som årsaker til at en ikke kunne registrere en eller flere av ordinalene i Ø-, V- eller M-vurderingene.">
				>
				["at0042"] = <
					text = <"Smertestimuli">
					description = <"Beskrivelse av type smertestimuli.">
					comment = <"For eksempel: 
\"Supraorbitalt trykk\" for sentral smertestimulering eller \"Trykk på negleroten\" for perifer smertestimulering. Trakealsuging og forflytning av pasient er andre eksempler på stimuli.">
				>
			>
		>
	>
