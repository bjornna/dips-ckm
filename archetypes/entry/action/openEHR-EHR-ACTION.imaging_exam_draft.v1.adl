archetype (adl_version=1.4; uid=6a9764e8-0424-4642-afc3-39f1d0786c28)
	openEHR-EHR-ACTION.imaging_exam_draft.v1

concept
	[at0000]	-- Billeddiagnostikk
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["organisation"] = <"Oslo University Hospital">
				["name"] = <"Ingrid Heitmann">
				["email"] = <"iheitman@ous-hf.no">
			>
			accreditation = <"Registered Nurse">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"2006-04-10">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record activity regarding the performance of imaging examinations.">
			use = <"Use to record activity regarding the performance of imaging examinations.">
			keywords = <"Radiology", "Ultrasound", "MRI", "CT", "scan", "tomography">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere aktiviteter knyttet til gjennomføringen av billeddiagnostikk">
			use = <"Brukes til å registrere aktiviteter knyttet til gjennomføringen av billeddiagnostikk.">
			keywords = <"Radiologi", "Ultralyd", "UL", "MR", "CT", "røngten", "tomografi", "røngtenfotograferingsteknikk">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"NotSet">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", ...>
	other_details = <
		["references"] = <"Derived from Imaging Examination (action) , draft archetype, National eHealth Transition Authority [Internet]. NEHTA Clinical Knowledge Manager. Authored: 08 Nov 2010. Available at: http://dcm.nehta.org.au/ckm/OKM.html#showarchetype_1013.1.999 (accessed Jan 16, 2012).


">
		["MD5-CAM-1.0.1"] = <"E5EB7FFD4C09ADBBDFAABF0006DC27AA">
	>

definition
	ACTION[at0000] matches {	-- Billeddiagnostikk
		ism_transition matches {
			ISM_TRANSITION[at0002] matches {	-- Undersøkelse planlagt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::526]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0002]}		-- Undersøkelse planlagt
					}
				}
			}
			ISM_TRANSITION[at0003] matches {	-- Undersøkelsesrekvisisjon sendt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::526]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0003]}		-- Undersøkelsesrekvisisjon sendt
					}
				}
			}
			ISM_TRANSITION[at0009] matches {	-- Undersøkelse utsatt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::527]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0009]}		-- Undersøkelse utsatt
					}
				}
			}
			ISM_TRANSITION[at0011] matches {	-- Undersøkelse avlyst
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::528]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0011]}		-- Undersøkelse avlyst
					}
				}
			}
			ISM_TRANSITION[at0004] matches {	-- Fastsatt tidspunkt for undersøkelse
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::529]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0004]}		-- Fastsatt tidspunkt for undersøkelse
					}
				}
			}
			ISM_TRANSITION[at0005] matches {	-- Undersøkelse replanlagt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::529]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0005]}		-- Undersøkelse replanlagt
					}
				}
			}
			ISM_TRANSITION[at0008] matches {	-- Undersøkelse utført
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::245]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0008]}		-- Undersøkelse utført
					}
				}
			}
			ISM_TRANSITION[at0014] matches {	-- Undersøkelse mislykket
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::245]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0014]}		-- Undersøkelse mislykket
					}
				}
			}
			ISM_TRANSITION[at0010] matches {	-- Undersøkelse midlertidig stanset
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::530]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0010]}		-- Undersøkelse midlertidig stanset
					}
				}
			}
			ISM_TRANSITION[at0012] matches {	-- Undersøkelse avbrutt
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::531]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0012]}		-- Undersøkelse avbrutt
					}
				}
			}
			ISM_TRANSITION[at0007] matches {	-- Undersøkelse fullført
				current_state matches {
					DV_CODED_TEXT matches {
						defining_code matches {[openehr::532]}
					}
				}
				careflow_step matches {
					DV_CODED_TEXT matches {
						defining_code matches {[local::at0007]}		-- Undersøkelse fullført
					}
				}
			}
		}
		description matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0017] occurrences matches {0..1} matches {	-- Undersøkelsesnavn
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0018] occurrences matches {0..1} matches {	-- Beskrivelse
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0019] occurrences matches {0..1} matches {	-- Begrunnelse
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0022] occurrences matches {0..*} matches {	-- Anatomisk lokalisering
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1/}
					}
					allow_archetype CLUSTER[at0021] occurrences matches {0..*} matches {	-- Undersøkelsesdetaljer
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0020] occurrences matches {0..1} matches {	-- Kommentar
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0015] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0016] occurrences matches {0..1} matches {	-- Start dato/tid
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Imaging examination">
					description = <"Clinical activity about performing an imaging examination.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Examination planned">
					description = <"Imaging examination is planned.">
				>
				["at0003"] = <
					text = <"Examination requested">
					description = <"Imaging examination has been requested.">
				>
				["at0004"] = <
					text = <"Appointment scheduled">
					description = <"Imaging examination appointment has been made.">
				>
				["at0005"] = <
					text = <"Appointment rescheduled">
					description = <"Appointment for imaging examination has been rescheduled.">
				>
				["at0007"] = <
					text = <"Examination complete">
					description = <"The imaging examination has been performed and all associated activities completed.">
				>
				["at0008"] = <
					text = <"Examination performed">
					description = <"The examation was performed but related activities not completed.">
				>
				["at0009"] = <
					text = <"Examination postponed">
					description = <"The examination has been postponed.">
				>
				["at0010"] = <
					text = <"Examination suspended">
					description = <"The examination has been suspended.">
				>
				["at0011"] = <
					text = <"Examination cancelled">
					description = <"The planned examination has been cancelled prior to commencement.">
				>
				["at0012"] = <
					text = <"Examination aborted">
					description = <"The examination has been aborted.">
				>
				["at0014"] = <
					text = <"Failed attempt">
					description = <"The examation was commenced but not completed successfully.">
				>
				["at0015"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0016"] = <
					text = <"Start date/time">
					description = <"The start date and/or time for the procedure. This will indicate the scheduled date/time when recorded against the 'Appointment scheduled' care pathway step or the actual Start date/time in the 'Examination performed' step.">
				>
				["at0017"] = <
					text = <"Examination name">
					description = <"The name of the examination (to be) performed. Coding of the specific procedure with a terminology is preferred, where possible.">
				>
				["at0018"] = <
					text = <"Description">
					description = <"Narrative description about the activity or care pathway step for the identified examination, for example description about the performance and findings from the the examination, the failed attempt or the cancellation of the examination.">
				>
				["at0019"] = <
					text = <"Reason">
					description = <"Reason that the activity or care pathway step for the identified examination was carried out, for example, the reason for the cancellation or suspension of the examination.">
				>
				["at0020"] = <
					text = <"Comment">
					description = <"Additional narrative about the activity or care pathway step not captured in other fields.">
				>
				["at0021"] = <
					text = <"Examination detail">
					description = <"Structured information about the examination. Use to capture detailed, structured information about method & technique etc.">
				>
				["at0022"] = <
					text = <"Anatomical location">
					description = <"Structured information about the specific anatomical location of the examination.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Billeddiagnostikk">
					description = <"Klinisk aktivitet knyttet til gjennomføringen av billeddiagnostikk.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Undersøkelse planlagt">
					description = <"Undersøkelsen er planlagt.">
				>
				["at0003"] = <
					text = <"Undersøkelsesrekvisisjon sendt">
					description = <"Det er sendt rekvisisjon for undersøkelsen.">
				>
				["at0004"] = <
					text = <"Fastsatt tidspunkt for undersøkelse">
					description = <"Tidspunkt for undersøkelsen er fastsatt.">
				>
				["at0005"] = <
					text = <"Undersøkelse replanlagt">
					description = <"Tidspunkt for undersøkelsen har blitt replanlagt.">
				>
				["at0007"] = <
					text = <"Undersøkelse fullført">
					description = <"Undersøkelsen er utført og alle tilknyttede kliniske handlinger er fullførte.">
				>
				["at0008"] = <
					text = <"Undersøkelse utført">
					description = <"Undersøkelsen, eller en del av undersøkelsen, har blitt utført.">
				>
				["at0009"] = <
					text = <"Undersøkelse utsatt">
					description = <"Undersøkelsen er utsatt.">
				>
				["at0010"] = <
					text = <"Undersøkelse midlertidig stanset">
					description = <"Undersøkelsen er suspendert/ midlertidig stanset.">
				>
				["at0011"] = <
					text = <"Undersøkelse avlyst">
					description = <"Den planlagte undersøkelsen har blitt avlyst før den ble igangsatt.">
				>
				["at0012"] = <
					text = <"Undersøkelse avbrutt">
					description = <"Undersøkelsen har blitt avbrutt.">
				>
				["at0014"] = <
					text = <"Undersøkelse mislykket">
					description = <"Undersøkelsen har blitt påstartet men ikke fullført.">
				>
				["at0015"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0016"] = <
					text = <"Start dato/tid">
					description = <"Dato og/eller tidspunkt for gjennomføringen av undersøkelsen. Registreringen vil, i forbindelse med prosesstrinnet \"Fastsatt tidspunkt for undersøkelse\" indikere den planlagte dato/tid for undersøkelsen eller den faktiske dato/tid for undersøkelsen ved bruk i forbindelse med prosesstrinnet \"Undersøkelse utført\".">
				>
				["at0017"] = <
					text = <"Undersøkelsesnavn">
					description = <"Navnet på undersøkelsen. Det bør om mulig benyttes terminologi for å angi undersøkelsesnavnet.">
				>
				["at0018"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av undersøkelsen, tilpasset det aktuelle prosesstrinnet. For eksempel en beskrivelse av utførelsen og funnene fra undersøkelsen, det avbrutte forsøket eller avlysningen av undersøkelsen.">
				>
				["at0019"] = <
					text = <"Begrunnelse">
					description = <"Begrunnelse for aktiviteten eller prosesstrinnet for den aktuelle undersøkelsen ble utført. For eksempel grunnen til at undersøkelsen ble avlyst eller midlertidig stanset.">
				>
				["at0020"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse av aktivitet eller prosesstrinn som ikke er registrert i andre felt.">
				>
				["at0021"] = <
					text = <"Undersøkelsesdetaljer">
					description = <"Strukturert informasjon om undersøkelsen. Bruk for å registrere detaljert, strukturert informasjon om metode og teknikker, utstyr som ble benyttet, funn etc.">
				>
				["at0022"] = <
					text = <"Anatomisk lokalisering">
					description = <"Strukturert informasjon om den spesifikke anatomiske lokaliseringen undersøkelsen har blitt utført på.">
				>
			>
		>
	>
