archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.problem_diagnosis.v1

concept
	[at0000]	-- Problem/Diagnosis
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Lars Bitsch-Larsen">
				["organisation"] = <"Haukeland University Hospital of Bergen, Norway">
			>
			accreditation = <"MD, DEAA, MBA, spec in anesthesia, spec in tropical medicine.">
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"23/04/2006">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details about a problem or diagnosis by a clinician.">
			use = <"Use to record detailed information about problems or diagnoses recognised by a clinician. There are many uses including: recording a Diagnosis during an Encounter; populating a Problem List or a Summary Statement, such as a Discharge Summary.

Use to record all problems or diagnoses, including those with context-specific qualifiers such as past/present, primary/secondary, active/inactive etc. These qualifiers can be archetyped separately and included in the ‘Status’ slot, because their use varies in different settings.">
			keywords = <"issue", "condition", "problem", "concern", "diagnosis", "criteria", "illness", "disease", "disorder">
			misuse = <"Not to be used to record 'Differential Diagnoses' - use the EVALUATION.differential diagnosis archetype.

Not to be used to record 'Reason for Encounter' - use the EVALUATION.reason_for_encounter archetype.

Not to be used to record 'Presenting Complaint' - which is information captured early in the encounter, usually prior to full assessment and will be represented using a separate EVALUATION archetype.

Not to be used to record procedures - use the ACTION.Procedure_undertaken archetype.

Not to be used to record symptoms or signs - these should be recorded as part of a patient story or history.  A problem such as 'Chest pain' may masquerade as a symptom, however in this context we are recording it as a problem the person has.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For registrering av detaljer om et problem eller diagnose av en kliniker.">
			use = <"Brukes til å registrere detaljert informasjon om problemer eller diagnoser som identifiseres av en kliniker. Det er mange bruksområder, blant annet: registrering av en diagnose under en konsultasjon; fylle en liste over problemer eller et sammendrag, som for eksempel en epikrise.

Brukes til å registrere alle problemer eller diagnoser, inkludert de med kontekstspesifikke kvalifikatorer som fortid / nåtid, primære / sekundære, aktiv / inaktiv etc. Disse kvalifikatorene kan registreres som separate arketyper og inngår i 'Status' slot, fordi deres bruk varierer i forskjellige situasjoner.">
			keywords = <"problem", "tilstand", "emne", "bekymring", "diagnose", "kriterier", "sykdom", "lidelse">
			misuse = <"Brukes ikke til å registrere \"differensialdiagnoser\" - bruk arketypen EVALUATION.differential_diagnosis.

Brukes ikke til å registrere \"kontaktårsak\" - bruk arketypen EVALUATION.reason_for_encounter. 

Brukes ikke ikke til å registrere \"hovedsymptom\" - som er informasjon som fås tidlig i konsultasjonen, vanligvis før man får gjort en fullstendig vurdering. Dette representeres ved hjelp av en egen EVALUATION-arketype. 

Brukes ikke til å registrere prosedyrer - bruk arketypen ACTION.Procedure_undertaken. 

Brukes ikke til å registrere symptomer eller sykdomstegn - disse skal registreres som en del av en pasienthistorie eller historie. Et problem som \"Brystsmerter\" kan være forkledd som et symptom, men i denne sammenhengen registreres det som et problem personen har.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <"Koray Atalag, University of Auckland, New Zealand", "John Bennett, NEHTA, Australia", "Steve Bentley, NHS CfH, United Kingdom", "Ian Bull, ACT Health, Australia", "Sergio Carmona, Chile", "Rong Chen, Cambio Healthcare Systems, Sweden", "Stephen Chu, NEHTA, Australia (Editor)", "Ed Conley, Cardiff University, United Kingdom", "Matthew Cordell, NEHTA, Australia", "Paul Donaldson, Nursing Informatics Australia, Australia", "Gail Easterbrook, Flinders Medical Centre, Australia", "David Evans, Queensland Health, Australia", "Peter Garcia-Webb, Australia", "Andrew Goodchild, NEHTA, Australia", "Anneke Goossen, Results4Care, Netherlands", "Heather Grain, Llewelyn Grain Consulting, Australia", "Trina Gregory, cpc, Australia", "Evelyn Hovenga, EJSH Consulting, Australia", "Eugene Igras, IRIS Systems Inc, Canada", "Mary Kelaher, NEHTA, Australia", "Eizen Kimura, Ehime University, Japan", "Shinji Kobayashi, Ehime University, Japan", "Robert L'egan, NEHTA, Australia", "Heather Leslie, Ocean Informatics, Australia (Editor)", "Hugh Leslie, Ocean Informatics, Australia (Editor)", "Rohan Martin, Ambulance Victoria, Australia", "David McKillop, NEHTA, Australia", "Ian McNicoll, Ocean Informatics UK, United Kingdom", "Chris Mitchell, RACGP, Australia", "Stewart Morrison, NEHTA, Australia", "Jörg Niggeman, Compugroup, Germany", "Andrej Orel, Marand d.o.o., Slovenia", "Chris Pearce, Melbourne East GP Network, Australia", "Camilla Preeston, Royal Australian College of General Practitioners, Australia", "Margaret Prichard, NEHTA, Australia", "Jodie Pycroft, Adelaide Northern Division of General Practice Ltd, Australia", "Cathy Richardson, NEHTA, Australia", "Robyn Richards, NEHTA - Clinical Terminology, Australia", "Thilo Schuler, Australia", "Gordon Tomes, Australian Institute of Health and Welfare, Australia", "Richard Townley-O'Neill, NEHTA, Australia", "Donna Truran, ACCTI-UoW, Australia", "Kylie Young, The Royal Australian College of General Practitioners, Australia">
	other_details = <
		["references"] = <"NEHTA Clinical data groups - Problem/Diagnosis V2.0 http://www.nehta.gov.au/data-group-library/data-specifications/dgl-problem-diagnosis.
Australian Standard AS 4700.6-2006 Implementation of Health Level Seven version 2.4 - Part 6: Referral, discharge and health record messaging.
CEN/TC 251/WGII  prEN 13940-2:2009.3- Health informatics – System of concepts to support Continuity of care – Part 2: Health care process and workflow.
BS_EN_13940-1_2007 - British Standard.  Health informatics - System of concepts to support continuity of care - Part 1: Basic concepts.
Swedish National Process model.
IHE Technical Framework Supplement – Patient Plan of Care (PPOC).
Royal Australian College of General Practitioners - Fact Sheet: Medical History
Mosby's Pocket Dictionary of Medicine, Nursing and Health Professional, 6th Edition, Mosby Elsevier, 2010">
		["MD5-CAM-1.0.1"] = <"70FC86F0BC3507E857C973D9943363C2">
	>

definition
	EVALUATION[at0000] matches {	-- Problem/Diagnosis
		data matches {
			ITEM_TREE[at0001] matches {	-- structure
				items cardinality matches {1..*; ordered} matches {
					ELEMENT[at0002] matches {	-- Problem/Diagnosis
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0009] occurrences matches {0..1} matches {	-- Clinical description
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0082] occurrences matches {0..1} matches {	-- Severity
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0010] occurrences matches {0..1} matches {	-- Date of onset
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0037] occurrences matches {0..1} matches {	-- Age at onset
						value matches {
							DV_DURATION matches {*}
						}
					}
					allow_archetype CLUSTER[at0044] occurrences matches {0..*} matches {	-- Anatomical location
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.anatomical_location(-[a-zA-Z0-9_]+)*\.v1/}
					}
					CLUSTER[at0018] occurrences matches {0..1} matches {	-- Occurrence summary
						items cardinality matches {1..*; ordered} matches {
							ELEMENT[at0025] occurrences matches {0..1} matches {	-- Number
								value matches {
									DV_COUNT matches {
										magnitude matches {|>=1|}
									}
								}
							}
							ELEMENT[at0019] occurrences matches {0..1} matches {	-- Frequency
								value matches {
									C_DV_QUANTITY <
										property = <[openehr::382]>
										list = <
											["1"] = <
												units = <"/min">
											>
											["2"] = <
												units = <"/h">
											>
											["3"] = <
												units = <"/d">
											>
											["4"] = <
												units = <"/wk">
											>
											["5"] = <
												units = <"/mo">
											>
											["6"] = <
												units = <"/yr">
											>
										>
									>
								}
							}
							ELEMENT[at0020] occurrences matches {0..1} matches {	-- Latest occurrence
								value matches {
									DV_DATE matches {*}
								}
							}
							ELEMENT[at0072] occurrences matches {0..1} matches {	-- Description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0073] occurrences matches {0..*} matches {	-- Link to occurrence details
								value matches {
									DV_URI matches {*}
								}
							}
						}
					}
					CLUSTER[at0026] occurrences matches {0..*} matches {	-- Related items
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0028] matches {	-- Related item
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0056] occurrences matches {0..1} matches {	-- Relationship type
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0080, 	-- Caused by
											at0081]	-- Following
										}
									}
								}
							}
						}
					}
					ELEMENT[at0030] occurrences matches {0..1} matches {	-- Date of resolution/remission
						value matches {
							DV_DATE matches {*}
						}
					}
					ELEMENT[at0031] occurrences matches {0..1} matches {	-- Age at resolution/remission
						value matches {
							DV_DURATION matches {*}
						}
					}
					allow_archetype CLUSTER[at0054] occurrences matches {0..*} matches {	-- Diagnostic criteria
						include
							archetype_id/value matches {/.*/}
					}
					allow_archetype CLUSTER[at0055] occurrences matches {0..*} matches {	-- Clinical stage/grade
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0069] occurrences matches {0..1} matches {	-- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0074] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0075] occurrences matches {0..1} matches {	-- Link to supporting clinical evidence
						value matches {
							DV_URI matches {*}
						}
					}
					allow_archetype CLUSTER[at0076] occurrences matches {0..*} matches {	-- Status
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Problem/Diagnosis">
					description = <"Any health care condition which may impact on the physical, mental and/or social well-being of an individual, that may require diagnostic, therapeutic or educational action, and which has been determined by a clinician. A diagnosis is based on scientific evaluation of physical signs, symptoms, history, laboratory tests results, and procedures.">
				>
				["at0001"] = <
					text = <"structure">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Problem/Diagnosis">
					description = <"Identification of the problem or diagnosis. Coding of the problem or diagnosis with a terminology is preferred, where possible.">
				>
				["at0009"] = <
					text = <"Clinical description">
					description = <"Narrative description or comments about clinical aspects of the problem/diagnosis.">
				>
				["at0010"] = <
					text = <"Date of onset">
					description = <"Estimated or actual date the problem/diagnosis began, in the opinion of the clinician.">
				>
				["at0018"] = <
					text = <"Occurrence summary">
					description = <"Summary information about occurrences or exacerbations. 
Note: Detailed information about each occurrence or exacerbation is likely to be held in other parts of the health record.">
				>
				["at0019"] = <
					text = <"Frequency">
					description = <"The frequency or estimated frequency of occurrences or exacerbations of the problem/diagnosis.">
				>
				["at0020"] = <
					text = <"Latest occurrence">
					description = <"The date of the last occurrence or exacerbation of the problem/diagnosis.">
				>
				["at0025"] = <
					text = <"Number">
					description = <"Cumulative number of occurrences or exacerbations of the problem/diagnosis.">
				>
				["at0026"] = <
					text = <"Related items">
					description = <"Further problems, diagnoses, procedures or events that are related in some way to this problem/diagnosis.">
				>
				["at0028"] = <
					text = <"Related item">
					description = <"Identification of a related problem, diagnosis, procedure, or event as text, coded text or link within the health record. Note: It is desirable that this field should be coded using terminology, wherever possible.">
				>
				["at0030"] = <
					text = <"Date of resolution/remission">
					description = <"The date or estimated date that the problem/diagnosis resolved or went into remission.">
				>
				["at0031"] = <
					text = <"Age at resolution/remission">
					description = <"The age of the person at the time of resolution or remission of the problem/diagnosis. 
Note: May be important in situations where approximations of age based on calculations are not accurate enough, eg in infants under one year.">
				>
				["at0037"] = <
					text = <"Age at onset">
					description = <"The estimated or actual age of the individual when the clinician assesses that the problem/diagnosis began. 
Note: May be important in situations where approximations of age based on calculations are not accurate enough, eg in infants under one year.">
				>
				["at0044"] = <
					text = <"Anatomical location">
					description = <"Slot to contain detailed and structured anatomical location details.">
				>
				["at0054"] = <
					text = <"Diagnostic criteria">
					description = <"The criteria on which the problem/diagnosis is based.">
				>
				["at0055"] = <
					text = <"Clinical stage/grade">
					description = <"Clinical stage or grade of a problem/diagnosis.   
Note: May include formal severity assessments.">
				>
				["at0056"] = <
					text = <"Relationship type">
					description = <"The type of relationship that this problem/diagnosis has to the related item.">
				>
				["at0069"] = <
					text = <"Comment">
					description = <"Additional narrative about the problem or diagnosis not captured in other fields.">
				>
				["at0072"] = <
					text = <"Description">
					description = <"A narrative description, including outcomes and other key details, about occurrences or exacerbations of the problem/diagnosis.">
				>
				["at0073"] = <
					text = <"Link to occurrence details">
					description = <"Link to further information about past occurrences or exacerbations of the problem/diagnosis that exist elsewhere in the health record.">
				>
				["at0074"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0075"] = <
					text = <"Link to supporting clinical evidence">
					description = <"Links to other relevant information, including pathology reports.">
				>
				["at0076"] = <
					text = <"Status">
					description = <"Descriptor for context- or use-case specific label or workflow-related aspect of the diagnostic process which may not be safe to exchange between systems or use in a shared environment. Examples include: active/inactive; primary/secondary; preliminary/provisional/working/final.">
				>
				["at0080"] = <
					text = <"Caused by">
					description = <"This concept identifies the direct cause or causative agent of a Problem/Diagnosis.  The concept includes the idea of ‘complications’, ‘causative agent’ and ‘due to’.  Note: Where no causality or sequence of events is known, this relationship type should be left blank.">
				>
				["at0081"] = <
					text = <"Following">
					description = <"This value identifies the sequence of events between the related items, but does not assert causality.  This can be used for sequelae or late effects.  Note: Where no causality or sequence of events is known, this relationship type should be left blank.">
				>
				["at0082"] = <
					text = <"Severity">
					description = <"A subjective assessment of the severity of the Problem/Diagnosis as evaluated by the clinician. Coding of the severity with a terminology is preferred, where possible.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Problem/diagnose">
					description = <"Enhver helsetilstand som kan påvirke den fysiske, psykiske og / eller sosiale velvære til en person, som kan kreve diagnostisk, terapeutisk eller pedagogisk handling ordinert av en kliniker. En diagnose er basert på vitenskapelig evaluering av fysiske tegn, symptomer, historie, laboratorieprøver og prosedyrer.">
				>
				["at0001"] = <
					text = <"structure">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Problem/diagnose">
					description = <"Identifikasjon av problemet eller diagnose. Koding av problemet eller diagnose med en terminologi foretrekkes, når det er mulig.">
				>
				["at0009"] = <
					text = <"Klinisk beskrivelse">
					description = <"En narrativ beskrivelse eller kommentar om kliniske aspekter av problemet/diagnosen.">
				>
				["at0010"] = <
					text = <"Dato for debut">
					description = <"Estimert eller faktisk dato der problem/diagnosen debuterte, etter klinikerens oppfatning.">
				>
				["at0018"] = <
					text = <"Forekomst">
					description = <"Sammendragsinformasjon om hendelser eller forverringer.

Merk: Detaljert informasjon om hver forekomst eller forverring vil sannsynligvis være registrert i andre deler av journalen.">
				>
				["at0019"] = <
					text = <"Hyppighet">
					description = <"Hyppigheten eller estimert hyppighet av forekomst eller forverring av problem/diagnose">
				>
				["at0020"] = <
					text = <"Seneste tilfelle">
					description = <"Dato for seneste tilfelle eller forverring av problem/diagnose.">
				>
				["at0025"] = <
					text = <"Antall">
					description = <"Kumulative antall av tilfeller eller forverringer ved problemet/diagnosen.">
				>
				["at0026"] = <
					text = <"Relaterte elementer">
					description = <"Ytterligere problemer, diagnoser, prosedyrer eller hendelser der på noen måte relaterer seg til problemet/diagnosen.">
				>
				["at0028"] = <
					text = <"Relaterte element">
					description = <"Identifikasjon av et relatert problem, diagnose, prosedyre eller hendelse som tekst, kode eller lenke innenfor journalen.
Merk: Det er ønskelig at dette felt bare brukes til koding når det er mulig.">
				>
				["at0030"] = <
					text = <"Dato for oppklaring / remisjon">
					description = <"Dato eller estimerte dato for oppklaring av problem/diagnose eller når det gikk i remisjon.">
				>
				["at0031"] = <
					text = <"Alder ved oppklaring/remisjon">
					description = <"Alderen til pasienten da problem/diagnose ble oppklart eller gikk i remisjon.
Merk: Kan være viktig i situasjoner når omtrentlig alder basert på beregninger ikke er nøyaktige nok, f.eks. spedbarn under 1 år.">
				>
				["at0037"] = <
					text = <"Alder ved debut">
					description = <"Den estimerte eller faktiske alder, ved debut av problem/diagnose, etter klinikerens oppfatning.
NB: Kan være viktig i situasjoner der estimeringer av alder basert på beregninger ikke er nøyaktige nok, som f.eks. hos spedbarn under ett år.">
				>
				["at0044"] = <
					text = <"Anatomisk område">
					description = <"SLOT for detaljert og strukturerte anatomiske detaljer.">
				>
				["at0054"] = <
					text = <"Diagnostiske kriterier">
					description = <"Kriterier problem/diagnose er basert på.">
				>
				["at0055"] = <
					text = <"Klinisk stadie">
					description = <"Klinisk stadie eller grad av problem/diagnose.
Merk: Kan inkludere formell vurdering av alvorlighetsgrad.">
				>
				["at0056"] = <
					text = <"Relasjonstype">
					description = <"Type relasjon som problemet/diagnosen har til det relaterte elementet.">
				>
				["at0069"] = <
					text = <"Kommentar">
					description = <"Ytterliger narrative opplysninger om problem eller diagnose som ikke er fanget opp i andre felter.">
				>
				["at0072"] = <
					text = <"Beskrivelse">
					description = <"En narrativ beskrivelse, inklusiv utfall og andre viktige detaljer og tilfeller eller forverringer av problem/diagnose.">
				>
				["at0073"] = <
					text = <"Lenke til tilfelle detaljer">
					description = <"Lenke til ytterligere informasjon om tidligere tilfelle eller forverringer av problemet/diagnosen som finnes andre steder i journalen.">
				>
				["at0074"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0075"] = <
					text = <"Lenke til understøttende klinisk evidens">
					description = <"Lenke til relevant informasjon, inklusiv laboratoriedata.">
				>
				["at0076"] = <
					text = <"Status">
					description = <"Beskrivelse for kontekst eller bruke-case bestemt etikett eller arbeidsflyt-relaterte aspekt av den diagnostiske prosessen som kanskje ikke er trygt å utveksle mellom systemer eller bruke i et felles miljø. Eksempler er: aktiv / passiv; primær / sekundær; foreløpige / provisorisk / arbeids- / endelig.">
				>
				["at0080"] = <
					text = <"Forårsaket av">
					description = <"Dette konseptet identifiserer den direkte årsaken til eller den utløsende faktoren for problemet/diagnosen. Conseptet inkluderer ideen om \"komplikasjoner\", \"utløsende årsak\" og \"på grunn av\". Merk: Når ingen utløsende årsak eller eller rekkefølge er kjent, bør denne relasjonstypen stå tom.">
				>
				["at0081"] = <
					text = <"Følger">
					description = <"Denne verdi identifiserer rekkefølgen av hendelser mellom relaterte elementer, men ikke nødvendigvis utløsende årsak. Dette kan brukes til sekveler eller senvirkninger. Merk: Hvor ingen utløsende årsak eller hendelser er kjent, bør denne relasjonstypen være tom.">
				>
				["at0082"] = <
					text = <"Alvorlighetsgrad">
					description = <"En subjektiv vurdering av alvorlighetsgraden til problemet/diagnosen slik det forstås av klinikeren. Koding med en terminologi foretrekkes der det er mulig.">
				>
			>
		>
	>
