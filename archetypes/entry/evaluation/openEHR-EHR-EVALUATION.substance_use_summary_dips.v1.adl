archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.substance_use_summary_dips.v1

concept
	[at0000]	-- Substance use summary DIPS
language
	original_language = <[ISO_639-1::nb]>
description
	original_author = <
		["name"] = <"Silje Ljosland Bakke">
		["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
		["organisation"] = <"Nasjonal IKT HF">
		["date"] = <"2017-02-03">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"To record summary information about the individual's pattern of substance use.">
			use = <"Use to record summary information about the individual's pattern of substance use.

This archetype is to be used to record information about both current and previous substance use behaviour.

The specific scope of this archetype is on documentation about the use of all types of addictive substances, except tobacco and alcohol. The \"Specific substance\" cluster allows for recording of specific details, routes and episodes about each type of substance used and can be repeated once per substance.

In some situations the individual will only consume one type of alcoholic beverage, such as beer. If other substances are used, the details will be recorded in another instance of the 'Specific substance' cluster.

The history of waxing and waning of use for each substance over time can be captured using the repeatable 'Per episode' cluster. This cluster of data elements allows for a very detailed pattern of substance use behaviour to be recorded for each type of substance used such as daily cannabis smoking, alongside MDMA use every Friday night and occasional cocaine use. 

Triggers for closing one episode and commencing a new one will largely reflect local data collection preferences, including if the individual: 
- quits for a significant period of time (which will likely be locally defined); or
- significantly changes their amount of use or pattern of their substance use.

Use to incorporate the narrative descriptions of substance use habits within existing or legacy clinical systems into an archetyped format, using the 'Overall description' data element.

This archetype is not approved outside of DIPS AS.">
			misuse = <"Not to be used to record information about tobacco use - use EVALUATION.tobacco_smoking_summary or EVALUATION.smokeless_tobacco_summary for this purpose.

Not to be used to record information about alcohol use - use EVALUATION.alcohol_use_summary for this purpose.

Not to be used to record information about the use of addictive substances as part of a normal diet, such as sugar, or caffeine in coffee or tea.

Not to be used to record event-or period-based information about substance use, such as actual daily use or the average use over a specified period of time - use the OBSERVATION.substance_use archetype.">
			copyright = <"">
		>
	>
	lifecycle_state = <"0">
	other_contributors = <>
	other_details = <
		["MD5-CAM-1.0.1"] = <"C97E52EE9C3AFA82748E4825704F0B52">
	>

definition
	EVALUATION[at0000] matches {	-- Substance use summary DIPS
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Overall substance use
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Injection substance use
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0028] occurrences matches {0..1} matches {	-- First ever injection substance use
						value matches {
							DV_DATE matches {
								value matches {yyyy-??-XX}
							}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Overall description
						value matches {
							DV_TEXT matches {*}
						}
					}
					CLUSTER[at0005] occurrences matches {0..*} matches {	-- Specific substance
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0006] matches {	-- Substance/substance group
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0007] occurrences matches {0..1} matches {	-- Overall substance status
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0008] occurrences matches {0..1} matches {	-- Overall substance description
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0009] occurrences matches {0..1} matches {	-- First use ever
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0010] occurrences matches {0..1} matches {	-- Regular use commenced
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0011] occurrences matches {0..1} matches {	-- Daily use commenced
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							CLUSTER[at0012] occurrences matches {0..*} matches {	-- Per route
								items cardinality matches {1..*; unordered} matches {
									ELEMENT[at0029] occurrences matches {0..1} matches {	-- Route
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0016] occurrences matches {0..1} matches {	-- Status
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {	-- Description
										value matches {
											DV_TEXT matches {*}
										}
									}
									CLUSTER[at0018] occurrences matches {0..*} matches {	-- Per episode
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0022] occurrences matches {0..1} matches {	-- Episode label
												value matches {
													DV_COUNT matches {
														magnitude matches {|>=0|}
													}
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0023] occurrences matches {0..1} matches {	-- Episode start date
												value matches {
													DV_DATE_TIME matches {*}
												}
											}
											ELEMENT[at0024] occurrences matches {0..1} matches {	-- Episode end date
												value matches {
													DV_DATE_TIME matches {*}
												}
											}
											ELEMENT[at0025] occurrences matches {0..1} matches {	-- Effects
												value matches {
													DV_TEXT matches {*}
												}
											}
											ELEMENT[at0026] occurrences matches {0..1} matches {	-- Typical use
												value matches {
													DV_TEXT matches {*}
													C_DV_QUANTITY <
														property = <[openehr::347]>
														list = <
															["1"] = <
																units = <"g/d">
																magnitude = <|>=0.0|>
																precision = <|1|>
															>
															["2"] = <
																units = <"gm/wk">
																magnitude = <|>=0.0|>
																precision = <|0|>
															>
															["3"] = <
																units = <"gm/mo">
																magnitude = <|>=0.0|>
																precision = <|0|>
															>
														>
													>
												}
											}
											ELEMENT[at0030] occurrences matches {0..1} matches {	-- Number of quit attempts
												value matches {
													DV_COUNT matches {
														magnitude matches {|>=0|}
													}
												}
											}
											allow_archetype CLUSTER[at0031] occurrences matches {0..*} matches {	-- Episode details
												include
													archetype_id/value matches {/.*/}
											}
											ELEMENT[at0027] occurrences matches {0..1} matches {	-- Form
												value matches {
													DV_TEXT matches {*}
												}
											}
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {	-- Quit date
										value matches {
											DV_DATE_TIME matches {*}
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {	-- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
							ELEMENT[at0013] occurrences matches {0..1} matches {	-- Combination use
								value matches {
									DV_TEXT matches {*}
								}
							}
							allow_archetype CLUSTER[at0032] occurrences matches {0..*} matches {	-- Type details
								include
									archetype_id/value matches {/.*/}
							}
							ELEMENT[at0015] occurrences matches {0..1} matches {	-- Frequency
								value matches {
									DV_TEXT matches {*}
									C_DV_QUANTITY <
										property = <[openehr::382]>
										list = <
											["1"] = <
												units = <"1/d">
												magnitude = <|>=0.0|>
												precision = <|0|>
											>
											["2"] = <
												units = <"1/wk">
												magnitude = <|>=0.0|>
												precision = <|0|>
											>
											["3"] = <
												units = <"1/mo">
												magnitude = <|>=0.0|>
												precision = <|0|>
											>
										>
									>
								}
							}
							ELEMENT[at0014] occurrences matches {0..1} matches {	-- Triggers
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Substance use summary DIPS">
					description = <"Summary or persistent information about an individual's addictive substances use.">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Overall substance use">
					description = <"Statement about current substance use behaviour for all addictive substances.">
				>
				["at0003"] = <
					text = <"Injection substance use">
					description = <"Statement about injection use behaviour for any addictive substances.">
				>
				["at0004"] = <
					text = <"Overall description">
					description = <"Narrative summary about the individual's overall substance use pattern and history.">
					comment = <"Use this data element to record a narrative description only where the structured data does not adequately reflect the substance use habits for this individual or to incorporate unstructured substance use information from existing or legacy clinical systems into an archetyped format.">
				>
				["at0005"] = <
					text = <"Specific substance">
					description = <"Details about use of a specific addictive substance.">
				>
				["at0006"] = <
					text = <"Substance/substance group">
					description = <"The specific substance or substance group used.">
				>
				["at0007"] = <
					text = <"Overall substance status">
					description = <"Statement about current use behaviour for the specific substance.">
				>
				["at0008"] = <
					text = <"Overall substance description">
					description = <"Narrative summary about use behaviour for the specified type of substance.">
				>
				["at0009"] = <
					text = <"First use ever">
					description = <"Date or partial date when the individual first used the specified substance.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0010"] = <
					text = <"Regular use commenced">
					description = <"The date or partial date when the individual first started frequent or regular, but usually non-daily, use of the substance.">
					comment = <"Can be a partial date, for example, only a year. For example, this date could represent when the individual commenced smoking cannabis every Friday night or at parties.">
				>
				["at0011"] = <
					text = <"Daily use commenced">
					description = <"The date or partial date when the individual first started daily use of the substance.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0012"] = <
					text = <"Per route">
					description = <"Details about the use of the specified substance through a specific route of administration.">
				>
				["at0013"] = <
					text = <"Combination use">
					description = <"Details about ">
				>
				["at0014"] = <
					text = <"Triggers">
					description = <"The typical pattern of use of the specified substance.">
				>
				["at0015"] = <
					text = <"Frequency">
					description = <"The frequency of use of the specified substance.">
				>
				["at0016"] = <
					text = <"Status">
					description = <"Statement about current use of the specified substance through the specified route.">
				>
				["at0017"] = <
					text = <"Description">
					description = <"Narrative summary about use behaviour for the specified type of substance through the specified route.">
				>
				["at0018"] = <
					text = <"Per episode">
					description = <"Details about a discrete period of consumption for the specified type of substance through the specified route.">
				>
				["at0019"] = <
					text = <"Quit date">
					description = <"The date or partial date the individual quit using the specified substance through the specified route.">
				>
				["at0021"] = <
					text = <"Comment">
					description = <"Additional narrative about use of the specified subtance, not captured in other fields.">
				>
				["at0022"] = <
					text = <"Episode label">
					description = <"Identification of an episode of substance use - either as a number in a sequence and/or a named event.">
					comment = <"For example: '2' as the second episode within a sequence of episodes; or 'Pregnancy with twins' if describing the substance use during a health event such as during a specific pregnancy.">
				>
				["at0023"] = <
					text = <"Episode start date">
					description = <"Date when this episode commenced.">
					comment = <"Can be a partial date, for example, only a year.">
				>
				["at0024"] = <
					text = <"Episode end date">
					description = <"Date when this episode ceased.">
					comment = <"Can be a partial date, for example, only a year. This data field will be empty if the episode is current and ongoing.">
				>
				["at0025"] = <
					text = <"Effects">
					description = <"The effects of the substance on the individual during the specified episode.">
				>
				["at0026"] = <
					text = <"Typical use">
					description = <"Estimate of the amount of the specified substance ">
				>
				["at0027"] = <
					text = <"Form">
					description = <"The form of the substance used.">
					comment = <"For example tablets or liquid.">
				>
				["at0028"] = <
					text = <"First ever injection substance use">
					description = <"The date or partial date when the individual first used any addictive substance via injection.">
				>
				["at0029"] = <
					text = <"Route">
					description = <"The specified route of administration.">
				>
				["at0030"] = <
					text = <"Number of quit attempts">
					description = <"Total number of times the individual has attempted to stop using the specified substance within this episode.">
				>
				["at0031"] = <
					text = <"Episode details">
					description = <"*">
				>
				["at0032"] = <
					text = <"Type details">
					description = <"Additional structured details about the use of the specified substance.">
				>
			>
		>
	>
