archetype (adl_version=1.4; uid=8d9cba91-c081-4b58-888b-da8e02c56ee8)
	openEHR-EHR-CLUSTER.clinical_evidence.v1

concept
	[at0000]	-- Clinical evidence
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Silje Ljosland Bakke">
				["organisation"] = <"Nasjonal IKT HF">
				["email"] = <"silje.ljosland.bakke@nasjonalikt.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"2015-01-23">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere eksplisitte detaljer om grunnlaget for et klinisk utsagn, for eksempel en problem/diagnose, risiko for overfølsomhetsreaksjon, helserisiko, eller kontraindikasjon.">
			use = <"Brukes for å registrere eksplisitte detaljer om grunnlaget for et klinisk utsagn, enten som en oppsummering av data som er tilgjengelig i andre arketyper (ofte av OBSERVATION-klassen), eller når de opprinnelige dataene ikke er tilgjengelige for applikasjonen.

Denne arketypen er ment å legges i et SLOT i arketypene EVALUATION.problem_diagnosis, EVALUATION.adverse_reaction_risk, EVALUATION.health_risk, EVALUATION.contraindication eller lignende oppsummerende arketyper, der utsagnet allerede er spesifisert i EVALUATION-arketypen. Den fungerer der som en valgfri utvidelse av datasettet i EVALUATION-arketypen.

Som spesifisert i openEHR referansemodellen kan det benyttes lenker for å lenke til data som ligger andre steder i journalen, mens denne arketypen er laget for å gjøre dette eksplisitt, og for å gjøre det mulig å vise og gjennomgå disse dataene klinisk der dette er relevant.

Grunnlaget kan legges direkte i denne i arketypen som et \"Funn\", eller referansen kan benyttes for å peke direkte til dataene som ligger et annet sted i journalen.">
			keywords = <"diagnose", "evidens", "tilstand", "sykdom", "problem", "observasjon", "begrunnelse">
			misuse = <"Skal ikke brukes til å registrere sammendrag om et problem eller en diagnose. For dette brukes arketypen EVALUATION.problem_diagnosis.

Skal ikke brukes til å registrere sammendrag om risiko for overfølsomhetsreaksjoner. For dette brukes arketypen EVALUATION.adverse_reaction_risk.

Skal ikke brukes til å registrere sammendrag om en helserisiko. For dette brukes arketypen EVALUATION.health_risk.

Skal ikke brukes til å registrere sammendrag om en kontraindikasjon. For dette brukes arketypen EVALUATION.contraindication.">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record an explicit finding in support of a clinical assertion, such as a problem/diagnosis, adverse reaction risk, health risk assessment, or contraindication.">
			use = <"Use to record details about findings that support a clinical assertion, either as a summary of data available in other (often OBSERVATION class) archetypes, or when the original observational data is not available.

This archetype is designed to be nested within a SLOT in the EVALUATION.problem_diagnosis, EVALUATION.adverse_reaction_risk, EVALUATION.health_risk, EVALUATION.contraindication, or similar summary archetypes, where the name of the assertion has already been specified in the EVALUATION, in order to extend its content with this additional and optional dataset. While LINKs within the openEHR Reference Model allow for linkage to data held elsewhere within the health record, this archetype has been developed to make this functionality explicit, and to enable clinical visibility and review of this data, where it may be relevant in a template or specification.

The clinical evidence can be entered directly into this archetype as a 'Finding', or a citation can be used to explicitly point to data within the health record.">
			keywords = <"diagnosis", "evidence", "condition", "disease", "problem", "assertion", "finding", "observation">
			misuse = <"Not to be used to record summary details about an identified problem or diagnosis - use the EVALUATION.problem_diagnosis for this purpose.

Not to be used to record summary details about an identified adverse reaction risk - use the EVALUATION.adverse_reaction_risk for this purpose.

Not to be used to record summary details about an identified health risk - use the EVALUATION.health_risk for this purpose.

Not to be used to record summary details about an identified contraindication - use the EVALUATION.contraindication for this purpose.">
			copyright = <"© openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"published">
	other_contributors = <"Morten Aas, Oslo Universitetssykehus, Norway", "Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Kristian Berg, Universitetssykehuset Nord Norge, Norway", "SBhusan Bhattacharyya, Sudisa Consultancy Services, India", "Ole Andreas Bjordal, Webmed, Norway", "Bjørn Christensen, Helse Bergen HF, Norway", "Chris Dickson, NHS Digital, United Kingdom", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Bente Gjelsvik, Helse Bergen, Norway", "Evelyn Hovenga, EJSH Consulting, Australia", "Hanne Joensen, Helse Bergen HUS, Norway", "Lars Morgan Karlsen, DIPS ASA, Norway", "Siri Larønningen, Kreftregisteret, Norway", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Hallvard Lærum, Direktoratet for e-helse, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Bjørn Næss, DIPS ASA, Norway", "Andrej Orel, Marand d.o.o., Slovenia", "Vladimir Pizzo, Hospital Sírio Libanês, Brazil", "Navin Ramachandran, NHS, United Kingdom", "Norwegian Review Summary, Nasjonal IKT HF, Norway", "Stein Arne Rimehaug, Sunnaas sykehus, Norway", "Raymond Simkus, Brookswood Family Practice, Canada", "Iztok Stotl, UKCLJ, Slovenia", "Nyree Taylor, Ocean Informatics, Australia", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)", "Mette Wam, Esito AS, Norway">
	other_details = <
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Ocean Informatics, heather.leslie@oceaninformatics.com">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"9B78CA663809B46AC41812F071954401">
		["build_uid"] = <"7c86af81-6bdc-471e-a5e6-26bd9d823391">
		["revision"] = <"1.0.0">
	>

definition
	CLUSTER[at0000] matches {	-- Clinical evidence
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0003] occurrences matches {0..1} matches {	-- Finding
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {	-- Finding description
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0005] occurrences matches {0..*} matches {*}
			ELEMENT[at0006] occurrences matches {0..1} matches {	-- Date identified
				value matches {
					DV_DATE_TIME matches {*}
				}
			}
			allow_archetype CLUSTER[at0007] occurrences matches {0..*} matches {	-- Citation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.citation(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0022] occurrences matches {0..1} matches {	-- Method
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Method description
				value matches {
					DV_TEXT matches {*}
				}
			}
			allow_archetype CLUSTER[at0018] occurrences matches {0..*} matches {	-- Multimedia representation
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.multimedia(-[a-zA-Z0-9_]+)*\.v1/}
			}
			ELEMENT[at0023] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}



ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Clinical evidence">
					description = <"Details about findings that support a clinical assertion.">
				>
				["at0001"] = <
					text = <"Method description">
					description = <"Narrative description of the method used to identify the evidence.">
				>
				["at0003"] = <
					text = <"Finding">
					description = <"Identification of the finding that supports the assertion.">
					comment = <"It is recommended that 'Finding' should be coded with a terminology, where possible.">
				>
				["at0004"] = <
					text = <"Finding description">
					description = <"Narrative description of the finding.">
				>
				["at0005"] = <
					text = <"Result">
					description = <"Measurement or value associated with the finding.">
					comment = <"For example: Mantoux test result.">
				>
				["at0006"] = <
					text = <"Date identified">
					description = <"The date on which the finding was clinically identified.">
					comment = <"For example: The date when the finding was made by or made available to the clinician.">
				>
				["at0007"] = <
					text = <"Citation">
					description = <"Detailed data available about the evidence held in another part of the health record.">
				>
				["at0018"] = <
					text = <"Multimedia representation">
					description = <"Digital image, video or diagram representing the clinical evidence.">
				>
				["at0022"] = <
					text = <"Method">
					description = <"Type of examination or investigation used to identify the evidence.">
					comment = <"Consider coding 'Method' with a terminology, where possible. For example: Physical examination, Chest x-ray, Histopathology result, Colonoscopy.">
				>
				["at0023"] = <
					text = <"Comment">
					description = <"Additional narrative about the finding not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Klinisk grunnlag">
					description = <"Grunnlaget for et klinisk utsagn.">
				>
				["at0001"] = <
					text = <"Metodebeskrivelse">
					description = <"Fritekstbeskrivelse av metoden som ble brukt for å komme fram til funnet.">
				>
				["at0003"] = <
					text = <"Funn">
					description = <"Funnet som støtter utsagnet.">
					comment = <"Det anbefales å kode \"Funn\" med en terminologi, der det er mulig.">
				>
				["at0004"] = <
					text = <"Beskrivelse av funn">
					description = <"Fritekstbeskrivelse av funnet.">
				>
				["at0005"] = <
					text = <"Resultat">
					description = <"Måling eller verdi knyttet til funnet.">
					comment = <"For eksempel mantouxtestresultat.">
				>
				["at0006"] = <
					text = <"Dato">
					description = <"Datoen da funnet ble klinisk anerkjent.">
					comment = <"For eksempel: Datoen da funnet ble gjort eller gjort tilgjengelig for klinikeren.">
				>
				["at0007"] = <
					text = <"Referanse">
					description = <"Detaljerte data om funnet i andre deler av journalen.">
				>
				["at0018"] = <
					text = <"Multimediarepresentasjon">
					description = <"Digitalt bilde, video eller diagram som representerer det kliniske grunnlaget.">
				>
				["at0022"] = <
					text = <"Metode">
					description = <"Typen undersøkelse eller utredning som er utført for å komme fram til funnet.">
					comment = <"Metoden bør vurderes kodet med en terminologi der dette er mulig. For eksempel fysisk undersøkelse, røntgen thorax, patologisvar, koloskopi.">
				>
				["at0023"] = <
					text = <"Kommentar">
					description = <"Ytterligere informasjon om funnet, som ikke kan registreres i andre felter.">
				>
			>
		>
	>
