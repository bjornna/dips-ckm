archetype (adl_version=1.4; uid=5fb25032-ee6e-4cb7-8bb9-8c1ae7b9fb84)
	openEHR-EHR-CLUSTER.occupation_dips.v1

concept
	[at0000]	-- Sysselsetting
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Kristian Berg">
				["organisation"] = <"Universitetssykehuset Nord Norge">
				["email"] = <"kristian.berg@unn.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["organisation"] = <"Ocean Informatics">
		["date"] = <"2010-12-17">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record information about the current occupation and occupation background of the individual.">
			use = <"Use to record information about the current occupation and occupation background of the individual.

This archetype has been specifically designed to be used in the 'Details' SLOT within the EVALUATION.social_summary archetype, but can also be used within any other ENTRY or CLUSTER archetypes, where clinically appropriate.">
			keywords = <"employment", "employer", "job", "occupation", "work", "profession", "unemployed", "employee", "unemployment">
			misuse = <"Not to be used for detailed descriptions of health risks or exposure to hazardous substances in the workplace. Use the archetypes EVALUATION.health_risk or EVALUATION.exposure for this purpose.

Not to be used to record information about sources of income or income details for the individual. Use the archetype CLUSTER.income for this purpose.">
			copyright = <"© 2010 NEHTA, openEHR Foundation, Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere informasjon om individets nåværende og tidligere sysselsettingsforhold.">
			use = <"Brukes for å registrere informasjon om individets nåværende og tidligere sysselsettingsforhold.

Arketypen er laget for å benyttes i SLOTet \"Detaljer\" i arketypen EVALUATION.social_summary (Sosialanamnese), men kan også brukes i andre ENTRY eller CLUSTER arketyper der det er klinisk relevant.">
			keywords = <"arbeid", "arbeidstaker", "arbeidsgiver", "arbeidsforhold", "arbeidshistorikk", "jobb", "ansatt", "yrke", "arbeidsløs">
			misuse = <"Brukes ikke for å beskrive helserisikoer eller eksponering for farlige substanser i arbeidssituasjonen. Til dette brukes henholdsvis arketypene EVALUATION.health_risk eller EVALUATION.exposure.

Brukes ikke for å registrere informasjon om individets inntektskilder eller detaljer om inntekt. Bruk arketypen CLUSTER.income for dette formålet.">
			copyright = <"© 2010 NEHTA, openEHR Foundation, Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"NotSet">
	other_contributors = <"Vebjørn Arntzen, Oslo universitetssykehus HF, Norway (Nasjonal IKT redaktør)", "Koray Atalag, University of Auckland, New Zealand", "Silje Ljosland Bakke, Nasjonal IKT HF, Norway (Nasjonal IKT redaktør)", "Marcus Baw, openGPSoC / BawMedical Ltd, United Kingdom", "Hildegard Franke, freshEHR Clinical Informatics Ltd., United Kingdom", "Sergio Freire, State University of Rio de Janeiro, Brazil", "Heather Grain, Llewelyn Grain Informatics, Australia", "Anne Gunn Haugland, Helse Bergen HF, Norway", "Kristian Heldal, Telemark Hospital Trust, Norway", "Susanna Jönsson, Landstinget i Värmland, Sweden", "Lars Morgan Karlsen, DIPS ASA, Norway", "Heather Leslie, Ocean Informatics, Australia (openEHR Editor)", "Siv Marie Lien, DIPS ASA, Norway", "Ian McNicoll, freshEHR Clinical Informatics, United Kingdom (openEHR Editor)", "Erik Nissen, Cambio Healthcare Systems AB, Sweden", "Andrej Orel, Marand d.o.o., Slovenia", "Martin Paulson, Sykehuset i Vestfold, Norway", "Tanja Riise, Nasjonal IKT HF, Norway", "Jon Tysdahl, Furst medlab AS, Norway", "John Tore Valand, Haukeland Universitetssjukehus, Norway (Nasjonal IKT redaktør)">
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["original_publisher"] = <"NEHTA, Australia">
		["build_uid"] = <"2ee89bf8-1390-4016-9d54-322ac9c1b5e5">
		["original_namespace"] = <"au.org.nehta">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["references"] = <"Derived from: Employment Summary, Draft Archetype [Internet]. nehta, Australia, nehta Clinical Knowledge Manager [cited: 2016-01-11]. Available from: http://dcm.nehta.org.au/ckm/#showArchetype_1013.1.925">
		["MD5-CAM-1.0.1"] = <"EAB9C384EFC7C780B357B6C23EDC8869">
		["custodian_namespace"] = <"org.openehr">
		["custodian_organisation"] = <"openEHR Foundation">
	>

definition
	CLUSTER[at0000] matches {	-- Sysselsetting
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Sysselsettingskategori
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0016] occurrences matches {0..1} matches {	-- Beskrivelse
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0002] occurrences matches {0..*} matches {	-- Sysselsettingsepisode
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {	-- Arbeidsgiver
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0004] occurrences matches {0..*} matches {	-- Detaljer om arbeidsgiver
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0005] occurrences matches {0..*} matches {	-- Tittel/rolle
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0013] occurrences matches {0..1} matches {	-- Stillingsandel
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::380]>
								list = <
									["1"] = <
										units = <"%">
										magnitude = <|0.0..100.0|>
										precision = <|2|>
									>
									["2"] = <
										units = <"1">
										magnitude = <|0.0..1.0|>
										precision = <|1|>
									>
								>
							>
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Klassifisering
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0015] occurrences matches {0..1} matches {	-- Status
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..1} matches {	-- Dato for oppstart
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {	-- Dato opphør
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0014] occurrences matches {0..1} matches {	-- Kommentar
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Occupation">
					description = <"Detailed information about the current occupation and occupation background of the individual.">
				>
				["at0001"] = <
					text = <"Occupation category">
					description = <"Current occupation category.">
					comment = <"For example: employed, unemployed, student, retired, etc. Coding with a terminology is desirable, where possible.">
				>
				["at0002"] = <
					text = <"Occupation episode">
					description = <"Details of each episode of occupation.">
				>
				["at0003"] = <
					text = <"Employer name">
					description = <"Identification of employer - company, organisation or individual.">
				>
				["at0004"] = <
					text = <"Employer details">
					description = <"Further detail about the Employer.">
				>
				["at0005"] = <
					text = <"Title/role">
					description = <"Job title or role of the occupation episode.">
				>
				["at0006"] = <
					text = <"Classification">
					description = <"Classification of the type of occupation undertaken by the individual.">
					comment = <"For example: categorisation according to the ISCO-08(http://www.ilo.org/global/publications/ilo-bookstore/order-online/books/WCMS_172572/lang--en/index.htm) major, sub-major, minor and unit groups..">
				>
				["at0007"] = <
					text = <"Date commenced">
					description = <"Date occupation episode commenced.">
				>
				["at0008"] = <
					text = <"Date ceased">
					description = <"Date occupation episode ceased.">
				>
				["at0013"] = <
					text = <"Full time equivalent">
					description = <"The position's fraction or percentage of a full time equivalent.">
				>
				["at0014"] = <
					text = <"Comment">
					description = <"Additional narrative about the occupation episode not captured in other fields.">
				>
				["at0015"] = <
					text = <"Status">
					description = <"The current status for this specific occupation episode.">
					comment = <"For example: on leave or sick leave, seconded, temporarily laid off. Coding with a terminology is desirable, where possible.">
				>
				["at0016"] = <
					text = <"Description">
					description = <"Narrative description of current and historical occupation.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Sysselsetting">
					description = <"Informasjon om individets nåværende og tidligere sysselsettingsforhold.">
				>
				["at0001"] = <
					text = <"Sysselsettingskategori">
					description = <"Nåværende sysselsettingskategori.">
					comment = <"For eksempel yrkesaktiv, arbeidsledig, student, pensjonert, etc. Koding med en terminologi er ønskelig der det er mulig.">
				>
				["at0002"] = <
					text = <"Sysselsettingsepisode">
					description = <"Detaljert informasjon om hver enkelt sysselsettingsepisode.">
				>
				["at0003"] = <
					text = <"Arbeidsgiver">
					description = <"Arbeidsgiver - selskap, organisasjon eller individ.">
				>
				["at0004"] = <
					text = <"Detaljer om arbeidsgiver">
					description = <"Ytterligere detaljer om arbeidsgiver.">
				>
				["at0005"] = <
					text = <"Tittel/rolle">
					description = <"Stillingstittel eller rolle for sysselsettingsepisoden.">
				>
				["at0006"] = <
					text = <"Klassifisering">
					description = <"Klassifisering av typen sysselsetting.">
					comment = <"For eksempel kategorisering i henhold til STYRK-08 (https://www.ssb.no/arbeid-og-lonn/artikler-og-publikasjoner/standard-for-yrkesklassifisering-styrk-08) \"yrkesfelt\", \"yrkesområder\", \"yrkesgrupper\" og \"yrker\".">
				>
				["at0007"] = <
					text = <"Dato for oppstart">
					description = <"Datoen sysselsettingsepisoden startet.">
				>
				["at0008"] = <
					text = <"Dato opphør">
					description = <"Datoen asysselsettingsepisoden opphørte.">
				>
				["at0013"] = <
					text = <"Stillingsandel">
					description = <"Stillingens andel av en fulltidsstilling.">
				>
				["at0014"] = <
					text = <"Kommentar">
					description = <"Ytterligere kommentar til sysselsettingsepisoden som ikke er fanget i andre felt.">
				>
				["at0015"] = <
					text = <"Status">
					description = <"Individets arbeidsstatus for denne spesifikke sysselsettingsepisoden.">
					comment = <"For eksempel sykemeldt, i aktiv sykemelding, i permisjon, utlånt eller permittert. Koding med en terminologi er ønskelig der det er mulig.">
				>
				["at0016"] = <
					text = <"Beskrivelse">
					description = <"Fritekstbeskrivelse av nåværende og tidligere sysselsettingsforhold.">
				>
			>
		>
	>
