archetype (adl_version=1.4; uid=04e4db1d-c4e2-4807-99ce-02ef94ab6d63)
	openEHR-EHR-CLUSTER.laboratory_test_panel_dips.v1

concept
	[at0000]	-- Laboratoriesvargruppe
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"Vebjørn Arntzen">
				["organisation"] = <"Oslo universitetssykehus HF">
				["email"] = <"varntzen@ous-hf.no">
			>
			accreditation = <"RN, Enterprise Architect TOGAF sertified">
		>
	>
description
	original_author = <
		["name"] = <"Ian McNicoll">
		["email"] = <"ian@freshehr.com">
		["organisation"] = <"freshEHR Clinical Informatics, UK">
		["date"] = <"2014-04-18">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record laboratory test results as a single value or in a panel/battery format common to clinical pathology testing such as biochemistry, haematology and immunology.">
			use = <"To record laboratory test results as a single value or in a panel/battery format common to clinical pathology testing such as biochemistry, haematology and immunology. Normally used in conjunction with a parent Laboratory test result (Observation) archetype.

The name attribute of the Laboratory Test Panel/Result value element is normally replaced in a template or at run-time to carry the name of the specific analyte e.g. 'Haemoglobin' 


">
			keywords = <"laboratory", "pathology", "panel", "battery", "analyte">
			misuse = <"Should not be used to record Anatomical pathology macroscopic/microscopic findings.">
			copyright = <"© Nasjonal IKT HF">
		>
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere laboratoriesvar, enten som en enkeltverdi eller i grupper/pakker i et format som er vanlig for klinisk analyse, som biokjemi, hematologi og immunologi.">
			use = <"For å registrere laboratoriesvar, enten som en enkeltverdi eller i grupper/pakker i et format som er vanlig for klinisk analyse, som biokjemi, hematologi og immunologi. Brukes vanligvis i sammenmeheng med en overordnet OBSERVATION-arketype for laboratorieprøvesvar.

Dataelementet \"Prøvesvar\" skal vanligvis omdøpes i templat eller i applikasjonen til den spesifikke analytten, f.eks \"Hemoglobin\".">
			keywords = <"laboratorie", "prøvepakker", "analyse", "analytt">
			misuse = <"Bør ikke brukes for å lagre funn fra patologi-undersøkelser der det er brukt makroskopiske eller mikroskopiske metoder.">
			copyright = <"© Nasjonal IKT HF">
		>
	>
	lifecycle_state = <"NotSet">
	other_contributors = <"Heather Leslie, Ocean Informatics, Australia", ...>
	other_details = <
		["revision"] = <"0.0.1-alpha">
		["original_publisher"] = <"Nasjonal IKT">
		["build_uid"] = <"6ecf2bb7-89f5-49a4-9012-d84e27c1de6f">
		["original_namespace"] = <"no.nasjonalikt">
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["references"] = <"Based on NEHTA 'Pathology Test' archetype. Available from: http://dcm.nehta.org.au/ckm/OKM.html#showarchetype_1013.1.839_8

Pathology (Data Specifications) Version 1.0 [Internet]. Sydney, Australia: National E-Health Transition Authority; 2007 May 29 [cited 2011 Jul 11]; Available at http://www.nehta.gov.au/component/docman/doc_download/962-pathology-v10.

Laboratory Technical Framework, Volume 3: Content, Revision 3.0 [Internet]. USA: IHE International; 2011 May 19; [cited 2011 Jul 11]. Available from: http://www.ihe.net/Technical_Framework/index.cfm#laboratory

Hl7 FHIR Observation resource: HL7 FHIR; Available from http://www.hl7.org/implement/standards/fhir/observation.html">
		["MD5-CAM-1.0.1"] = <"DDD91367ABECF4348874797F86FBFE79">
		["custodian_namespace"] = <"no.nasjonalikt">
		["custodian_organisation"] = <"Nasjonal IKT">
	>

definition
	CLUSTER[at0000] matches {	-- Laboratoriesvargruppe
		items cardinality matches {1..*; unordered} matches {
			CLUSTER[at0002] occurrences matches {0..*} matches {	-- Analysesvar
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0001] occurrences matches {0..1} matches {*}
					ELEMENT[at0003] occurrences matches {0..*} matches {	-- Kommentar
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] occurrences matches {0..1} matches {	-- Veiledning til referanseområdet
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Svarstatus
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0007, 	-- Registrert
									at0008, 	-- Foreløpig
									at0009, 	-- Endelig
									at0010, 	-- Endret
									at0011, 	-- Kansellert
									at0012]	-- Ikke bestilt
								}
							}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {	-- Tidspunkt for svarstatus
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0013] occurrences matches {0..*} matches {	-- Andre detaljer
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.specimen(-[a-zA-Z0-9_]+)*\.v1/}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Laboratory Test Panel">
					description = <"Laboratory test result as a single value or in a panel/battery format common to clinical pathology testing.">
				>
				["at0001"] = <
					text = <"Result value">
					description = <"Actual value of the result. The name of this element is normally replaced in a template or at run-time to relfect the actual analyte e.g. Serum sodium and will often be coded.">
					hl7v2_mapping = <"OBX.2,OBX.5,OBX.6; OBX-3-observation identifier">
					fhir_mapping = <"Observation.result; Observation.name">
				>
				["at0002"] = <
					text = <"Laboratory result">
					description = <"Specific detailed result, including both the value of the result item, and additional information that may be useful for clinical interpretation.">
				>
				["at0003"] = <
					text = <"Comment">
					description = <"Comment about the Result.">
					hl7v2_mapping = <"NTE.3">
					fhir_mapping = <"Observation.comments">
				>
				["at0004"] = <
					text = <"Reference range guidance">
					description = <"Additional advice on the applicability of the reference range.">
				>
				["at0005"] = <
					text = <"Result status">
					description = <"The status of the result value.">
					hl7v2_mapping = <"OBX-11-observation result status">
					fhir_mapping = <"status">
				>
				["at0006"] = <
					text = <"Result status timestamp">
					description = <"The date and/or time that the entire result was issued for the recorded ‘Result status’.">
					hl7v2_mapping = <"OBX-22 and/or OBX-19">
					fhir_mapping = <"Observation.issued">
				>
				["at0007"] = <
					text = <"Registered">
					description = <"No result yet available.">
				>
				["at0008"] = <
					text = <"Interim">
					description = <"This is an initial or interim result: data may be missing or verification not been performed.">
				>
				["at0009"] = <
					text = <"Final">
					description = <"The result is complete and verified by the responsible pathologist.">
				>
				["at0010"] = <
					text = <"Amended">
					description = <"The result has been modified subsequent to being Final, and is complete and verified by the responsible pathologist.">
				>
				["at0011"] = <
					text = <"Cancelled/Aborted">
					description = <"The result is unavailable because the test was not started or not completed.">
				>
				["at0012"] = <
					text = <"Not requested">
					description = <"The result is not available as the test was not requested.">
				>
				["at0013"] = <
					text = <"Other detail">
					description = <"Further details including the specimen for the panel or a further nested panel.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Laboratoriesvargruppe">
					description = <"Laboratoriesvar som en enkeltverdi eller i grupper/pakker som er vanlig i kliniske prøveanalyser.">
				>
				["at0001"] = <
					text = <"Svarverdi">
					description = <"Faktisk verdi av prøvesvaret. Navnet på dette dataelementet er vanligvis erstattet i templat eller i applikasjon, slik at det samsvarer med den faktiske analysen. For eksempel \"S-Na\" eller \"Serum Natrium\". Det vil også som regel være kodet ved hjelp av et kodeverk.">
				>
				["at0002"] = <
					text = <"Analysesvar">
					description = <"Spesifikt detaljert svar, som inkluderer både verdien og tilleggsinformasjon som kan være nyttig for klinisk tolkning.">
				>
				["at0003"] = <
					text = <"Kommentar">
					description = <"Kommentar til svaret.">
				>
				["at0004"] = <
					text = <"Veiledning til referanseområdet">
					description = <"Tilleggsinformasjon med råd for referanseområdets hensiktsmessighet.">
				>
				["at0005"] = <
					text = <"Svarstatus">
					description = <"Svarverdiens status">
				>
				["at0006"] = <
					text = <"Tidspunkt for svarstatus">
					description = <"Dato og/eller tidspunkt hele svaret ble utstedt for den registrerte Svarstatus.">
				>
				["at0007"] = <
					text = <"Registrert">
					description = <"Ikke tilgjengelig svar ennå.">
				>
				["at0008"] = <
					text = <"Foreløpig">
					description = <"Dette er et midlertidig eller foreløpig svar. Data kan mangle, eller være uverifisert.">
				>
				["at0009"] = <
					text = <"Endelig">
					description = <"Svaret er komplett og verifisert av ansvarlig laboratorielege.">
				>
				["at0010"] = <
					text = <"Endret">
					description = <"Svaret er endret etter å ha hatt status Endelig, og er komplett og verifisert av ansvarlig laboratorielege.">
				>
				["at0011"] = <
					text = <"Kansellert">
					description = <"Svaret er ikke tilgjengelig fordi analysen ikke ble påbegynt eller ferdigstilt.">
				>
				["at0012"] = <
					text = <"Ikke bestilt">
					description = <"Svaret er ikke tilgjengelig siden analysen ikke ble bestilt.">
				>
				["at0013"] = <
					text = <"Andre detaljer">
					description = <"Ytterligere detaljer, inkludert informasjon om prøveeksemplaret i analysepakker, eller når materialet deles opp i flere forskjellige analyser.">
				>
			>
		>
	>
